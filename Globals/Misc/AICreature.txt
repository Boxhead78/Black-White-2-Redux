///////////////////////////////////////////////////////////////////////////////
//          Creature AI Script
//    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

//Delay for miracle against nearby enemies
define CT_CreatureMiracleDelayMin = 5
define CT_CreatureMiracleDelayMax = 60
//Delay for high range miracles
define CT_CreatureMiracleAggressiveDelayMin = 90
define CT_CreatureMiracleAggressiveDelayMax = 120
//Maximum waittime until creature is released from forced action
define CT_ActionFinishedSeconds = 13

define script CT_CreatureAI(PlayerID)

global FireballAvailable[4]
global LightningAvailable[4]
global FireballMedRangeAvailable[4]
global FireballHighRangeAvailable[4]

run script CT_ScriptManager


begin script CT_ScriptManager

start

    run background script CT_CreatureAI(0)
    run background script CT_CreatureAI(1)
    run background script CT_CreatureAI(2)
    run background script CT_CreatureAI(3)

end script CT_ScriptManager


begin script CT_CreatureAI(PlayerID)

    CT_Creature = 0
    EnemyID = 0
    EnemyObject = 0
    ThrowMiracleTimer = create timer for 120 seconds
    ThrowMiracleTimerAggressive = create timer for 120 seconds

start

begin loop
    wait 5 seconds

    if get research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available == variable RESEARCH_AVAILABILITY_RESEARCHED and PlayerID == 0 and FireballAvailable[PlayerID] == 0
        FireballAvailable[PlayerID] = 1
    end if

    if get research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available == variable RESEARCH_AVAILABILITY_RESEARCHED and PlayerID == 0 and LightningAvailable[PlayerID] == 0
        LightningAvailable[PlayerID] = 1
    end if

    if get research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available == variable RESEARCH_AVAILABILITY_RESEARCHED and get research ARTEFACT_CREATURE_ROLE_SOLDIER_2 available == variable RESEARCH_AVAILABILITY_RESEARCHED and PlayerID == 0 and FireballMedRangeAvailable[PlayerID] == 0
        FireballMedRangeAvailable[PlayerID] = 1
    end if

    if get research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available == variable RESEARCH_AVAILABILITY_RESEARCHED and get research ARTEFACT_CREATURE_ROLE_SOLDIER_3 available == variable RESEARCH_AVAILABILITY_RESEARCHED and PlayerID == 0 and FireballHighRangeAvailable[PlayerID] == 0
        FireballHighRangeAvailable[PlayerID] = 1
    end if

    if EnemyID > 4
        EnemyID = 0
        wait until (get ThrowMiracleTimer time remaining <= 0 or get ThrowMiracleTimerAggressive time remaining <= 0) and not leash is in hand and HEALTH of CT_Creature > 0
    end if
    
    CT_Creature = get player PlayerID creature
    if PlayerID != EnemyID and HEALTH of CT_Creature > 0
        //Close Range Enemy Platoon
        EnemyObject = get platoon of player EnemyID nearest {CT_Creature} radius 40
        if EnemyObject exists and not leash is in hand
            if get ThrowMiracleTimer time remaining <= 0
                ThrowMiracleTimer = create timer for number from CT_CreatureMiracleDelayMin to CT_CreatureMiracleDelayMax seconds
                if number from 1 to 3 == 1 and FireballAvailable[PlayerID] == 1
                    force CT_Creature CREATURE_CAST_FIREBALL EnemyObject
                elsif LightningAvailable[PlayerID] == 1
                    force CT_Creature CREATURE_CAST_LIGHTNING_BOLT EnemyObject
                end if
                wait until creature CT_Creature finished script action or CT_ActionFinishedSeconds seconds or leash is in hand
                release CT_Creature
            end if
        else
            //Close Range Enemy Siege
            EnemyObject = get siege weapon of player EnemyID nearest {CT_Creature} radius 40
            if EnemyObject exists and not leash is in hand
                if get ThrowMiracleTimer time remaining <= 0
                    ThrowMiracleTimer = create timer for number from CT_CreatureMiracleDelayMin to CT_CreatureMiracleDelayMax seconds
                    if number from 1 to 3 == 1 and FireballAvailable[PlayerID] == 1
                        force CT_Creature CREATURE_CAST_FIREBALL EnemyObject
                    elsif LightningAvailable[PlayerID] == 1
                        force CT_Creature CREATURE_CAST_LIGHTNING_BOLT EnemyObject
                    end if
                    wait until creature CT_Creature finished script action or CT_ActionFinishedSeconds seconds or leash is in hand
                    release CT_Creature
                end if
            elsif FireballMedRangeAvailable[PlayerID] == 1
                //Med Range Enemy Platoon
                EnemyObject = get platoon of player EnemyID nearest {CT_Creature} radius 75
                if EnemyObject exists and not leash is in hand
                    if get ThrowMiracleTimer time remaining <= 0
                        ThrowMiracleTimer = create timer for number from CT_CreatureMiracleDelayMin to CT_CreatureMiracleDelayMax seconds
                        force CT_Creature CREATURE_CAST_FIREBALL EnemyObject
                        wait until creature CT_Creature finished script action or CT_ActionFinishedSeconds seconds or leash is in hand
                        release CT_Creature
                    end if
                else
                    //Med Range Enemy Siege
                    EnemyObject = get siege weapon of player EnemyID nearest {CT_Creature} radius 75
                    if EnemyObject exists and not leash is in hand
                        if get ThrowMiracleTimer time remaining <= 0
                            ThrowMiracleTimer = create timer for number from CT_CreatureMiracleDelayMin to CT_CreatureMiracleDelayMax seconds
                            force CT_Creature CREATURE_CAST_FIREBALL EnemyObject
                            wait until creature CT_Creature finished script action or CT_ActionFinishedSeconds seconds or leash is in hand
                            release CT_Creature
                        end if
                    elsif FireballHighRangeAvailable[PlayerID] == 1
                        //High Range Enemy Platoon
                        EnemyObject = get platoon of player EnemyID nearest {CT_Creature} radius 150
                        if EnemyObject exists and not leash is in hand
                            if get ThrowMiracleTimerAggressive time remaining <= 0
                                ThrowMiracleTimerAggressive = create timer for number from CT_CreatureMiracleDelayMin to CT_CreatureMiracleDelayMax seconds
                                force CT_Creature CREATURE_CAST_FIREBALL EnemyObject
                                wait until creature CT_Creature finished script action or CT_ActionFinishedSeconds seconds or leash is in hand
                                release CT_Creature
                            end if
                        else
                            //High Range Enemy Siege
                            EnemyObject = get siege weapon of player EnemyID nearest {CT_Creature} radius 150
                            if EnemyObject exists and not leash is in hand
                                if get ThrowMiracleTimerAggressive time remaining <= 0
                                    ThrowMiracleTimerAggressive = create timer for number from CT_CreatureMiracleAggressiveDelayMin to CT_CreatureMiracleAggressiveDelayMax seconds
                                    force CT_Creature CREATURE_CAST_FIREBALL EnemyObject
                                    wait until creature CT_Creature finished script action or CT_ActionFinishedSeconds seconds or leash is in hand
                                    release CT_Creature
                                end if
                            else
                                //Special Actions against Altars and stuff
                                EnemyObject = get HOUSE ALTAR at {CT_Creature} radius 175
                                if EnemyObject not exists or PLAYER of EnemyObject == PlayerID
                                    EnemyObject = get HOUSE TEMPLE at {CT_Creature} radius 175
                                    if EnemyObject not exists or PLAYER of EnemyObject == PlayerID
                                        EnemyObject = get HOUSE MELEE_ARMOURY at {CT_Creature} radius 175
                                        if EnemyObject not exists or PLAYER of EnemyObject == PlayerID
                                            EnemyObject = get HOUSE RANGED_ARMOURY at {CT_Creature} radius 175
                                            if EnemyObject not exists or PLAYER of EnemyObject == PlayerID
                                                EnemyObject = get HOUSE at {CT_Creature} radius 175
                                            end if
                                        end if
                                    end if
                                end if
                                if ALIGNMENT of CT_Creature <= 0.25 and EnemyObject exists and not leash is in hand and PLAYER of EnemyObject == EnemyID
                                    if get ThrowMiracleTimerAggressive time remaining <= 0
                                        ThrowMiracleTimerAggressive = create timer for number from CT_CreatureMiracleAggressiveDelayMin to CT_CreatureMiracleAggressiveDelayMax seconds
                                        force CT_Creature CREATURE_CAST_FIREBALL EnemyObject
                                        wait until creature CT_Creature finished script action or CT_ActionFinishedSeconds seconds or leash is in hand
                                        release CT_Creature
                                    end if
                                end if
                            end if
                        end if
                    end if
                end if
            end if
        end if
    end if
    EnemyID++
end loop

end script CT_CreatureAI