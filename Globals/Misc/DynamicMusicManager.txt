///////////////////////////////////////////////////////////////////////////////
//       Dynamic Music Manager Script
//    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

define DMM_MAX_PLAYERS = MAX_PLAYERS
define DMM_CHECKS_DELAY = 5
define DMM_PAUSE_SECONDS = 300
define DMM_STOP_MUSIC_SECONDS = 30
define DMM_ACTIVE_RADIUS = 425

define DMM_STOP_MUSIC = 1

define DMM_MUSIC_PAUSE_ON = 1
define DMM_MUSIC_PAUSE_OFF = 2

define DMM_SERVERITY_LEVEL_NONE = 0
define DMM_SERVERITY_LEVEL_1 = 1
define DMM_SERVERITY_LEVEL_2 = 2
define DMM_SERVERITY_LEVEL_3 = 3
define DMM_SERVERITY_LEVEL_4 = 4
define DMM_SERVERITY_LEVEL_MAX = 5

define script DMM_DynamicMusicManager(battleMusicPause, battleMusicSeverity, raceID)
define script DMM_DynamicMusicManagerPlayMusic(whichTown, playerID, enemyPlayerID, raceID)

global DMM_IsRunning = 0
global DMM_StopMusic = 0 //DMM_StopMusic is used to kill the script DMM_DynamicMusicManager
global DMM_HaltScript = 0 //DMM_HaltScript is used to make sure to play no other dynamic music and wait in DMM_DynamicMusicManager until DMM_HaltScript is 0
global DMM_Severity = 0
global DMM_Pause = 0
global DMM_MusicIsStopping = 0
global DMM_HowOftenIsThisScriptActive = 0
global DMM_ChangeMusic = 0
global DMM_MusicIsRunning = 0

begin script DMM_DynamicMusicManager(battleMusicPause, battleMusicSeverity, raceID)

    Counter = 0
    myTown = 0
    myPlatoon = 0
    enemyPlatoon = 0
    enemyBuilding = 0
    numberOfPlatoons = 0
    numberOfEnemyPlatoons = 0
    scriptQueueTime = 0

start

DMM_HowOftenIsThisScriptActive++
scriptQueueTime = DMM_HowOftenIsThisScriptActive

wait until DMM_MusicIsStopping == 0

wait scriptQueueTime seconds //If this script is called multiple times make sure to set the data of the last in queue

DMM_Severity = battleMusicSeverity
if DMM_Severity >= DMM_SERVERITY_LEVEL_MAX
    DMM_Severity = DMM_SERVERITY_LEVEL_4
end if

DMM_Pause = battleMusicPause
if DMM_Pause != DMM_MUSIC_PAUSE_ON and DMM_Pause != DMM_MUSIC_PAUSE_OFF
    DMM_Pause = DMM_MUSIC_PAUSE_ON
end if

DMM_HowOftenIsThisScriptActive--

if DMM_HowOftenIsThisScriptActive == 0
    DMM_ChangeMusic = 1
end if

if DMM_IsRunning == 0
    DMM_IsRunning = 1
    begin loop
        if Counter > 15
            Counter = 0
            wait DMM_CHECKS_DELAY seconds
        end if

        myTown = get town with id Counter
        myPlatoon = 0
        enemyPlatoon = 0
        if myTown exists and size of myTown > 0 and PLAYER of myTown >= 0 and PLAYER of myTown <= DMM_MAX_PLAYERS-1
            myPlatoon = get platoon of player 0 nearest {myTown} radius DMM_ACTIVE_RADIUS
            // Get enemy platoon
            if DMM_MAX_PLAYERS >= 4
                if enemyPlatoon == 0 or enemyPlatoon not exists
                    enemyPlatoon = get platoon of player 3 nearest {myTown} radius DMM_ACTIVE_RADIUS
                end if
            end if
            if DMM_MAX_PLAYERS >= 3
                if enemyPlatoon == 0 or enemyPlatoon not exists
                    enemyPlatoon = get platoon of player 2 nearest {myTown} radius DMM_ACTIVE_RADIUS
                end if
            end if
            if DMM_MAX_PLAYERS >= 2
                if enemyPlatoon == 0 or enemyPlatoon not exists
                    enemyPlatoon = get platoon of player 1 nearest {myTown} radius DMM_ACTIVE_RADIUS
                end if
            end if

            numberOfPlatoons = 0
            numberOfEnemyPlatoons = 0
            if myPlatoon exists
                numberOfPlatoons = get number of platoon of player 0 at {myPlatoon} radius DMM_ACTIVE_RADIUS/4
            end if
            if enemyPlatoon exists
                numberOfEnemyPlatoons = get number of platoon of player PLAYER of enemyPlatoon at {enemyPlatoon} radius DMM_ACTIVE_RADIUS/4
                //Only get platoons if no enemy building is nearby
                enemyBuilding = get HOUSE at {enemyPlatoon} radius 25
                if enemyBuilding exists and PLAYER of enemyBuilding != PLAYER of enemyPlatoon
                    enemyBuilding = 0
                end if
                if enemyBuilding exists and PLAYER of enemyBuilding == PLAYER of enemyPlatoon
                    enemyPlatoon = 0
                    numberOfEnemyPlatoons = 0
                end if
            end if

            //Minimum requirements to play music
            if myPlatoon exists and (get number of villagers in platoon myPlatoon >= 50 or numberOfPlatoons > 2) and get size of myPlatoon action queue >= 1 and PLAYER of myTown >= 1 and PLAYER of myTown <= DMM_MAX_PLAYERS-1 and get player PLAYER of myPlatoon influence at {myPlatoon} == 0
                run script DMM_DynamicMusicManagerPlayMusic(myTown, 0, 0, raceID)

            elsif enemyPlatoon exists and (get number of villagers in platoon enemyPlatoon >= 80 or numberOfEnemyPlatoons > 4) and get size of enemyPlatoon action queue >= 1 and PLAYER of myTown == 0 and get player PLAYER of enemyPlatoon influence at {enemyPlatoon} == 0
                run script DMM_DynamicMusicManagerPlayMusic(myTown, 0, PLAYER of enemyPlatoon, raceID)

            end if
        end if

        wait until DMM_HaltScript == 0 or DMM_StopMusic == 1

        Counter++
        until DMM_StopMusic == 1
    end loop
    DMM_IsRunning = 0
end if

end script DMM_DynamicMusicManager

begin script DMM_DynamicMusicManagerPlayMusic(whichTown, playerID, enemyPlayerID, raceID)

    musicMixer = 0
    Number = 0
    numberOfPlayerPlatoons = 0
    numberOfEnemyPlatoons = 0
    myPlatoon = 0
    enemyBuildingNearPlatoon = 0
    enemyBuildingNearSiege = 0
    enemyPlatoon = 0
    mySiegeWeapon = 0
    enemySiegeWeapon = 0
    battleMusicLevel = 0
    selectedBattleMusicLevel = 0
    waitTime = 0
    changeMusicTimer = create timer for 0 seconds
    conditionTrue = 0
    conditionActive[9]
    StopScriptTimer = create timer for 0 seconds
    StopScriptTimerInitiated = 0
    MusicIntro = 0
    MusicIntroMax = 2
    StopMusicTimer = create timer for 0 seconds
    SkipMusic = number from 1 to 5
    SkipSet = 0
    MaximumPlayTimer = create timer for 300 seconds
    MinimumPlayTimer = create timer for 45 seconds
    whichTownPlayer = PLAYER of whichTown

start

    //Sometimes skip small themes
    if SkipSet == 0 and SkipMusic == 5
        SkipSet = 1
        battleMusicLevel = DMM_Severity
        selectedBattleMusicLevel = DMM_Severity
    end if

    DMM_MusicIsRunning = 1
    begin loop
        wait until DMM_HaltScript == 0 or DMM_StopMusic == 1

        //INIT
        myPlatoon = 0
        enemyPlatoon = 0
        numberOfPlayerPlatoons = 0
        numberOfEnemyPlatoons = 0
        wait DMM_CHECKS_DELAY seconds

        //GET PLATOONS
        myPlatoon = get platoon of player playerID nearest {whichTown} radius DMM_ACTIVE_RADIUS*1.25
        numberOfPlayerPlatoons = get number of platoon of player playerID at {myPlatoon} radius DMM_ACTIVE_RADIUS/4*1.25
        if enemyPlayerID != 0
            enemyPlatoon = get platoon of player enemyPlayerID nearest {whichTown} radius DMM_ACTIVE_RADIUS*1.25
            numberOfEnemyPlatoons = get number of platoon of player enemyPlayerID at {enemyPlatoon} radius DMM_ACTIVE_RADIUS/4*1.25
            //Only get platoons if no enemy building is nearby
            enemyBuildingNearPlatoon = get HOUSE at {enemyPlatoon} radius 25
            if enemyBuildingNearPlatoon exists and PLAYER of enemyBuildingNearPlatoon != PLAYER of enemyPlatoon
                enemyBuildingNearPlatoon = 0
            end if
            if enemyBuildingNearPlatoon exists and PLAYER of enemyBuildingNearPlatoon == PLAYER of enemyPlatoon
                enemyPlatoon = 0
                numberOfEnemyPlatoons = 0
            end if
        end if

        //GET SIEGE WEAPONS
        mySiegeWeapon = get siege weapon of player playerID nearest {whichTown} radius DMM_ACTIVE_RADIUS*1.25
        if enemyPlayerID != 0
            enemySiegeWeapon = get siege weapon of player enemyPlayerID nearest {whichTown} radius DMM_ACTIVE_RADIUS*1.25
            //Only get siege if no enemy building is nearby
            enemyBuildingNearSiege = get HOUSE at {enemySiegeWeapon} radius 25
            if enemyBuildingNearSiege exists and PLAYER of enemyBuildingNearSiege != PLAYER of enemySiegeWeapon
                enemyBuildingNearSiege = 0
            end if
            if enemyBuildingNearSiege exists and PLAYER of enemyBuildingNearSiege == PLAYER of enemySiegeWeapon
                enemySiegeWeapon = 0
            end if
        end if

        //DO CHECKS AND PLAY MUSIC
        //Player single Platoon check
        if myPlatoon exists and get number of villagers in platoon myPlatoon >= 50 and get player playerID influence at {myPlatoon} == 0
            if conditionActive[0] == 0
                conditionActive[0] = 1
                conditionTrue++
            end if
        elsif conditionActive[0] == 1
            conditionActive[0] = 0
            conditionTrue--
        end if
        //Player number of Platoon check
        if myPlatoon exists and numberOfPlayerPlatoons > 2 and get player playerID influence at {myPlatoon} == 0
            if conditionActive[1] == 0
                conditionActive[1] = 1
                conditionTrue++
            end if
        elsif conditionActive[1] == 1
            conditionActive[1] = 0
            conditionTrue--
        end if
        //Enemy single Platoon check
        if enemyPlatoon exists and get number of villagers in platoon enemyPlatoon >= 80 and get player enemyPlayerID influence at {enemyPlatoon} == 0
            if conditionActive[2] == 0
                conditionActive[2] = 1
                conditionTrue++
            end if
        elsif conditionActive[2] == 1
            conditionActive[2] = 0
            conditionTrue--
        end if
        //Enemy number of Platoon check
        if enemyPlatoon exists and numberOfEnemyPlatoons > 4 and get player enemyPlayerID influence at {enemyPlatoon} == 0
            if conditionActive[3] == 0
                conditionActive[3] = 1
                conditionTrue++
            end if
        elsif conditionActive[3] == 1
            conditionActive[3] = 0
            conditionTrue--
        end if
        //Player siege weapon check
        if mySiegeWeapon exists and get player playerID influence at {mySiegeWeapon} == 0
            if conditionActive[4] == 0
                conditionActive[4] = 1
                conditionTrue++
            end if
        elsif conditionActive[4] == 1
            conditionActive[4] = 0
            conditionTrue--
        end if
        //Enemy siege weapon check
        if enemySiegeWeapon exists and get player enemyPlayerID influence at {enemySiegeWeapon} == 0
            if conditionActive[5] == 0
                conditionActive[5] = 1
                conditionTrue++
            end if
        elsif conditionActive[5] == 1
            conditionActive[5] = 0
            conditionTrue--
        end if
        //Minimum play timer check
        if get MinimumPlayTimer time remaining > 0
            if conditionActive[6] == 0
                conditionActive[6] = 1
                conditionTrue++
            end if
        elsif conditionActive[6] == 1
            conditionActive[6] = 0
            conditionTrue--
        end if

        if conditionTrue >= 1 and get StopMusicTimer time remaining < 60
            StopMusicTimer = create timer for 999 seconds
        elsif conditionTrue == 0 and get StopMusicTimer time remaining > 30
            StopMusicTimer = create timer for 30 seconds
        end if

        if DMM_ChangeMusic == 1
            DMM_ChangeMusic = 0
            changeMusicTimer = create timer for 0 seconds
        end if

        if (StopScriptTimerInitiated == 0 and DMM_HaltScript == 0 and DMM_StopMusic == 0) and (get StopMusicTimer time remaining > 0 or battleMusicLevel != selectedBattleMusicLevel)
            while get changeMusicTimer time remaining <= 0
                if (get changeMusicTimer time remaining <= 0) and (DMM_Severity == DMM_SERVERITY_LEVEL_1 or battleMusicLevel != selectedBattleMusicLevel or battleMusicLevel == 0)
                    battleMusicLevel = 1
                    selectedBattleMusicLevel = 1
                    Number = number from 1 to 2
                    run background script DM_SetDynamicMusic(DM_Small,DM_Random,raceID,0)

                    waitTime = number from 10 to 30
                    changeMusicTimer = create timer for waitTime seconds
                end if

                if get changeMusicTimer time remaining <= 0 and battleMusicLevel >= 1 and DMM_Severity == DMM_SERVERITY_LEVEL_2
                    battleMusicLevel = 2
                    selectedBattleMusicLevel = 2
                    Number = number from 1 to 5
                    if Number == 1
                        run background script DM_SetDynamicMusic(DM_Medium,DM_mediumbattle01,raceID,1)

                    elsif Number == 2
                        run background script DM_SetDynamicMusic(DM_Medium,DM_mediumbattle02,raceID,1)

                    elsif Number == 3
                        run background script DM_SetDynamicMusic(DM_Medium,DM_mediumbattle03,raceID,1)

                    elsif Number == 4
                        run background script DM_SetDynamicMusic(DM_Medium,DM_mediumbattle04,raceID,1)

                    elsif Number == 5
                        run background script DM_SetDynamicMusic(DM_Medium,DM_mediumbattle05,raceID,1)

                    end if

                    waitTime = number from 60 to 120
                    changeMusicTimer = create timer for waitTime seconds
                end if

                if get changeMusicTimer time remaining <= 0 and battleMusicLevel >= 1 and DMM_Severity == DMM_SERVERITY_LEVEL_3
                    battleMusicLevel = 3
                    selectedBattleMusicLevel = 3

                    Number = number from 1 to 5
                    if Number == 1
                        run background script DM_SetDynamicMusic(DM_Big,DM_bigbattle01,raceID,1)

                    elsif Number == 2
                        run background script DM_SetDynamicMusic(DM_Big,DM_bigbattle02,raceID,1)

                    elsif Number == 3
                        run background script DM_SetDynamicMusic(DM_Big,DM_bigbattle03,raceID,1)

                    elsif Number == 4
                        run background script DM_SetDynamicMusic(DM_Big,DM_bigbattle04,raceID,1)

                    elsif Number == 5
                        run background script DM_SetDynamicMusic(DM_Big,DM_bigbattle05,raceID,1)

                    end if

                    waitTime = number from 100 to 150
                    changeMusicTimer = create timer for waitTime seconds
                end if

                if get changeMusicTimer time remaining <= 0 and battleMusicLevel >= 1 and DMM_Severity == DMM_SERVERITY_LEVEL_4
                    battleMusicLevel = 4
                    selectedBattleMusicLevel = 4

                    waitTime = number from 60 to 120
                    changeMusicTimer = create timer for waitTime seconds
                end if
            end while
        end if

        //Stop music if enemy is attacking and maximum play timer is reached
        if DMM_HaltScript == 0 and DMM_StopMusic == 0 and get MaximumPlayTimer time remaining <= 0 and enemyPlayerID != 0
            battleMusicLevel = DMM_SERVERITY_LEVEL_1
            if get StopScriptTimer time remaining <= 0
                run background script DM_SetDynamicMusic(DM_Small,DM_Random,raceID,0)
                StopScriptTimerInitiated = 1
                StopScriptTimer = create timer for DMM_STOP_MUSIC_SECONDS+DMM_CHECKS_DELAY seconds
            end if
        end if

        //Fade out music if town is captured
        if DMM_HaltScript == 0 and DMM_StopMusic == 0 and PLAYER of whichTown != whichTownPlayer
            battleMusicLevel = DMM_SERVERITY_LEVEL_1
            if get StopScriptTimer time remaining <= 0
                run background script DM_SetDynamicMusic(DM_Small,DM_Random,raceID,0)
                StopScriptTimerInitiated = 1
                StopScriptTimer = create timer for DMM_STOP_MUSIC_SECONDS+DMM_CHECKS_DELAY seconds
            end if
        end if

        //No conditions are true, fade out music
        if DMM_HaltScript == 0 and DMM_StopMusic == 0 and get StopMusicTimer time remaining <= 0 //Initiate stop music
            battleMusicLevel = DMM_SERVERITY_LEVEL_1
            if get StopScriptTimer time remaining <= 0
                run background script DM_SetDynamicMusic(DM_Small,DM_Random,raceID,0)
                StopScriptTimerInitiated = 1
                StopScriptTimer = create timer for DMM_STOP_MUSIC_SECONDS+DMM_CHECKS_DELAY seconds
            end if
        end if

        if DMM_HaltScript == 1 or DMM_StopMusic == 1 or (get StopScriptTimer time remaining <= DMM_CHECKS_DELAY+1 and StopScriptTimerInitiated == 1)
            //end music
            DMM_MusicIsStopping = 1
            if DMM_HaltScript == 0
                run background script DM_StopDynamicMusic(7)
                wait 7 seconds
                wait 3 seconds
                if DMM_StopMusic == 0
                    if DMM_Pause == DMM_MUSIC_PAUSE_ON
                        wait until DMM_StopMusic == 1 or DMM_HowOftenIsThisScriptActive >= 1 or DMM_PAUSE_SECONDS seconds
                        wait 5 seconds
                    elsif DMM_Pause == DMM_MUSIC_PAUSE_OFF
                        wait until DMM_StopMusic == 1 or 5 seconds
                    end if
                end if
            end if
            DMM_MusicIsRunning = 0
            DMM_MusicIsStopping = 0
        end if

        until DMM_MusicIsRunning == 0
    end loop

end script DMM_DynamicMusicManagerPlayMusic