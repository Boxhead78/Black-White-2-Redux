///////////////////////////////////////////////////////////////////////////////
//            Player Villagers
//    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

define PLAYERVILLAGERSDELAY = 15

define script AAV_PlayerVillagers

run script AAV_PlayerVillagers

begin script AAV_PlayerVillagers

    PlayerTownLocal = 0
    HowManyVillagers = 0
    TownCentre = 0
    TownSize = 0
    TownCapacity = 0
    TownCounter = 0
    TownHouse = 0
    Villager = 0
    VillagerCounter = 0

start

begin loop

    if TownCounter > 16
        TownCounter = 0
    end if

    PlayerTownLocal = get town with id TownCounter
    TownCentre = get building ABODE_NUMBER_TOWN_CENTRE in PlayerTownLocal min built 1.0
    if PLAYER of PlayerTownLocal == 0 and TownCentre exists and variable get town PlayerTownLocal status != variable TOWN_STATUS_MIGRATION_STARTED
        TownSize = size of PlayerTownLocal
        TownCapacity = adult capacity of PlayerTownLocal
        VillagerCounter = 0
        if TownCapacity > TownSize * 1.5
            begin loop
                TownHouse = get random home in town PlayerTownLocal
                if TownHouse exists
                    Villager = create CHILD constant from VILLAGER_INFO_GREEK_HOUSEWIFE_FEMALE to VILLAGER_INFO_GREEK_TRADER_MALE at {TownHouse}
                else
                    Villager = create CHILD constant from VILLAGER_INFO_GREEK_HOUSEWIFE_FEMALE to VILLAGER_INFO_GREEK_TRADER_MALE at {TownCentre}
                end if
                attach Villager to PlayerTownLocal
                release Villager
                wait number from 4 to 6 seconds
                VillagerCounter++
                until VillagerCounter > TownSize * 0.25 or adult capacity of PlayerTownLocal < size of PlayerTownLocal
            end loop
            wait PLAYERVILLAGERSDELAY seconds
        end if
    end if

    TownCounter++
end loop

end script AAV_PlayerVillagers