//--------------------------------------------------------------------------------------------------------------
define script ThunderstormSpell(PlayerID,SpellPosition)
define script Thunderstorm_Clouds(PlayerID,ThunderstormSpell)
define script Thunderstorm_Lightning(ThunderstormSpell,Weather)
define script Thunderstorm_Miracles(PlayerID,ThunderstormSpell,Weather)

begin script ThunderstormSpell(PlayerID,SpellPosition)
    Rock = 0
    SeedEffect = 0
    PlayerMana = 0
    ThunderstormSpell = 0

    BreakLoop = 0

    Effect1 = 0

start

    if PlayerID == 0
        //For players
        empty player hand
        wait 0.25 seconds
        Rock = create SCRIPT_OBJECT_TYPE_MOBILE_STATIC MOBILE_STATIC_INFO_BOULDER_ROUND at hand position
        SeedEffect = create visual effect VISUAL_EFFECT_EPIC_VOLCANO_SEED on Rock time -1
        set SeedEffect colour red 110 green 110 blue 110
        set Rock in player 0 hand
        ThunderstormSpell = Rock
        PlayerMana = get player PlayerID mana

        set player PlayerID mana PlayerMana - 30000

        wait 1 seconds

        begin loop
            if bindable action BINDABLE_ACTION_TYPE_ACTION performed
                BreakLoop = 1  
                SpellPosition = marker at hand position
            end if
            if not Rock is HELD
                delete Rock
                delete SeedEffect
                BreakLoop = 2
            end if
            until BreakLoop >= 1
        end loop

        delete Rock
        delete SeedEffect
    else
        // For AIs
        BreakLoop = 1
    end if

    if BreakLoop == 1
        //Script here
        Effect1 = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_AGGRESSOR at {SpellPosition} + {0,10,0} time 40
        set Effect1 colour red 192 green 192 blue 192
        run background script Thunderstorm_Clouds(PlayerID,marker at {SpellPosition})
        wait 5 seconds
        Effect1 = create visual effect VISUAL_EFFECT_EPIC_TARGET_WARNING at {SpellPosition} time 15 target {SpellPosition}
        set Effect1 colour red 192 green 192 blue 192
        Effect1 = create visual effect VISUAL_EFFECT_EPIC_VOLCANO_BEAM_PULSE_DOWN at {SpellPosition}+{0,200,0} time 1
        set Effect1 colour red 192 green 192 blue 192
        if camera position near {SpellPosition} radius 2500 / 4 play string sound "SCRIPT1_VEPIC_DECAYCHARGESPELL" end if
        wait 5 seconds
        Effect1 = create visual effect VISUAL_EFFECT_EPIC_VOLCANO_BEAM_PULSE_DOWN at {SpellPosition}+{0,200,0} time 1
        set Effect1 colour red 192 green 192 blue 192
        if camera position near {SpellPosition} radius 2500 / 4 play string sound "SCRIPT1_VEPIC_DECAYCHARGESPELL" end if
        wait 5 seconds
        Effect1 = create visual effect VISUAL_EFFECT_EPIC_VOLCANO_BEAM_PULSE_DOWN at {SpellPosition}+{0,200,0} time 1
        set Effect1 colour red 192 green 192 blue 192
        if camera position near {SpellPosition} radius 2500 / 4 play string sound "SCRIPT1_VEPIC_DECAYCHARGESPELL" end if
        wait 5 seconds
    elsif BreakLoop == 2
        PlayerMana = get player PlayerID mana
        set player PlayerID mana PlayerMana + 30000
    end if

end script ThunderstormSpell


begin script Thunderstorm_Clouds(PlayerID,ThunderstormSpell)

    myWeather = 0

start

myWeather = create SCRIPT_OBJECT_TYPE_WEATHER_THING WEATHER_INFO_MEDIUM_CLOUD at {ThunderstormSpell} + {0, 0, 0}
set cloud for myWeather above {ThunderstormSpell}
set cloud generation 7
set myWeather properties rainfall 1 snowfall 0 overcast 0.9
set cloud myWeather points to 7

wait 50 seconds

run background script Thunderstorm_Miracles(PlayerID,marker at {ThunderstormSpell},myWeather)
run background script Thunderstorm_Lightning(marker at {ThunderstormSpell},myWeather)

wait 70 seconds

delete myWeather

end script Thunderstorm_Clouds


begin script Thunderstorm_Lightning(ThunderstormSpell,Weather)

    Altar = 0
    House = 0
    LightningPos = 0
    Prohab = 0
    Town = 0

start

Town = get TOWN at {ThunderstormSpell} radius 250

begin loop
    if Town exists
        LightningPos = marker at {ThunderstormSpell} + {number from -80 to 80,0,number from -80 to 80}
        Altar = get HOUSE ALTAR at {LightningPos} radius 25
        if Altar not exists
            set lightning strike at {LightningPos} from Weather scale 1
            add effect EFFECT_TYPE_BURN at {LightningPos} strength 0.75 radius 10
        end if
        Altar = 0
        House = get random home in town Town excluding scripted
        wait number from 4 to 6 seconds
        Prohab = number from 1 to 3
        if Prohab == 1 or Prohab == 2
            if House exists
                Altar = get HOUSE ALTAR at {House} radius 25
                if Altar not exists
                    set lightning strike at {House} from Weather scale 1
                    add effect EFFECT_TYPE_BURN at {House} strength 0.75 radius 10
                end if
                Altar = 0
            end if
        end if
    else
        LightningPos = marker at {ThunderstormSpell} + {number from -80 to 80,0,number from -80 to 80}
        set lightning strike at {LightningPos} from Weather scale 1
        Altar = get HOUSE ALTAR at {LightningPos} radius 25
        if Altar not exists
            set lightning strike at {LightningPos} from Weather scale 1
            add effect EFFECT_TYPE_BURN at {LightningPos} strength 0.75 radius 10
        end if
        Altar = 0
    end if

    wait number from 6 to 10 seconds

    until Weather not exists
end loop

end script Thunderstorm_Lightning


begin script Thunderstorm_Miracles(PlayerID,ThunderstormSpell,Weather)

    WindWonderStart = 0
    WindWonderTarget = 0
    WindWonder = 0

start

begin loop
    WindWonderStart = marker at ({Weather} + {0, 500, 0} + {number from -75 to 75, number from 485 to 515, number from -75 to 75})
    WindWonderTarget = marker at ({WindWonderStart} + {number from -5 to 5, number from -5 to -15, number from -5 to 5})
    WindWonder = make player PlayerID throw miracle MIRACLE_TYPE_TORNADO from ({WindWonderStart} + {0,-3.0,0}) heading ({WindWonderTarget} + {0,-21,0} - {WindWonderStart})

    wait number from 4 to 5 seconds

    until Weather not exists
end loop

end script Thunderstorm_Miracles