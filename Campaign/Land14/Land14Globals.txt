///////////////////////////////////////////////////////////////////////////////
//    Land 14 Globals
//    ~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

// Other Defines
define oCreature = 0
define script Globals
define script LC14_CreateUndeadPlatoon(whichPlatoon,howManyVillagersToDelete,howManyVillagersToSpawn,whichMesh)
define script LC14_GhostScript(myGhost,whichMesh)
define script LC14_AIAltarCounter(whichBuilding)

define s_aztecwarrior_ghost = 0
define s_skeletonadultland2 = 1
define s_undeadwarrior_noghost = 2

// Debug switch
global DEBUG = 0
global Intro = 1

global LC14_EnemyCreature = 0
global CinemaIsRunning = 0
global MigrationsAccepted = 0
global TownsConverted = 0
global TownsAggressiveConverted = 0
global TownsPeacefulConverted = 0
global UnderAttack = 0
global IntroFinished = 0
global DeleteWeather = 0
global AttackingPlatoons = 0
global GenericCommentTimer = 0
global UnitDeadTimer = 0
global AztecTemple[3]
global AztecTempleScrolls[3]
global TalusCommentWall = 0
global TempleDestroyed[3]
global EarlyAttackWaveAllowed = 0
global LC14_LargeSiegeIsRunning = 0
global LC14_LargeSiegeLaunchAttack = 0
global LC14_LargeSiegeOver = 0
global DynamicMusicReady = 0
global AIVolcano = 0
global AIEarthquake = 0
global AIHurricane = 0
global EpicRunning = 0
global VolcanoActive = 0
global HurricaneActive = 0
global VolcanoStopFireBalls = 0
global LandOver = 0
global AIAttacksAllowed = 0
global myAttackTimer = 0
global DeleteGodOnceAndForAll = 0
global MusicIsRunning = 0
global VolcanoFired = 0
global LC14_AltarCounter = 0

global PlayerTown = 0
global AztecTown1 = 0
global AztecTown2 = 0
global AztecTown3 = 0
global AztecCapital = 0
global NearestSettlement = 0
global PlayerTempTown = 0
global AztecTempleTown[3]
global PlayerCreatureTown = 0

global AZTECTOWN1_IMPRESSIVENESS = 6500
global AZTECTOWN2_IMPRESSIVENESS = 40000
global AZTECTOWN3_IMPRESSIVENESS = 999999 //Scrapped, only a Temple now
global AZTECCAPITAL_IMPRESSIVENESS = 34000

global AttackNearestTownTimer = 0
global LC14_RecruitTimer1 = 0
global LC14_RecruitTimer2 = 0
global LC14_RecruitTimer3 = 0

begin script Globals

SecondPlaythrough = 0

start

    GenericCommentTimer = create timer for 0 seconds
    UnitDeadTimer = create timer for 200 seconds
    myAttackTimer = create timer for 0 seconds

    AZTECTOWN1_IMPRESSIVENESS = AZTECTOWN1_IMPRESSIVENESS
    AZTECTOWN2_IMPRESSIVENESS = AZTECTOWN2_IMPRESSIVENESS *  ImpressivenessModifier
    AZTECTOWN3_IMPRESSIVENESS = AZTECTOWN3_IMPRESSIVENESS *  ImpressivenessModifier
    AZTECCAPITAL_IMPRESSIVENESS = AZTECCAPITAL_IMPRESSIVENESS *  ImpressivenessModifier

    SecondPlaythrough = persistent data "SECOND_PLAYTHROUGH"

    if SecondPlaythrough == 1
        AZTECTOWN1_IMPRESSIVENESS += 4000
        AZTECTOWN2_IMPRESSIVENESS += 4000
        AZTECCAPITAL_IMPRESSIVENESS += 4000
    end if

    GB_PlayerTown = get town with id 3//For Global Script
    PlayerTown = get town with id 3
    AztecTown1 = get town with id 2
    AztecTown2 = get town with id 4
    AztecTown3 = get town with id 6
    AztecCapital = get town with id 0
    PlayerTempTown = get town with id 7
    AztecTempleTown[0] = get town with id 5
    AztecTempleTown[1] = get town with id 4
    AztecTempleTown[2] = get town with id 6
    PlayerCreatureTown = get town with id 8

end script Globals


//Only works with global platoons
begin script LC14_CreateUndeadPlatoon(whichPlatoon,howManyVillagersToDelete,howManyVillagersToSpawn,whichMesh)

    Ghost[400]
    counter = 0
    myEffect = 0
    Villager = 0
    VillagerTemp = 0

start

    attach atmos "ghost_legion" to whichPlatoon range 40

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {whichPlatoon} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {whichPlatoon} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 1.0

    myEffect = create visual effect VISUAL_MIRACLE_HEAL_GROUND at {whichPlatoon} + {0,2,0} time 12
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    //Remove old villagers
    while counter < howManyVillagersToDelete - 1 and size of whichPlatoon > 1
        if counter < howManyVillagersToDelete
            Villager = get villager from platoon whichPlatoon nearest {whichPlatoon}
            myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Villager} + {0,1,0} time 1.5
            set myEffect color red 148 green 0 blue 211
            SCALE of myEffect = 0.1
            delete Villager
        end if
        counter++
    end while

    Villager = get villager from platoon whichPlatoon nearest {whichPlatoon}
    counter = 0
    while counter < howManyVillagersToSpawn

        //Create ghosts
        Ghost[counter] = create VILLAGER AZTEC_FORESTER at {whichPlatoon} + {number from -10 to 10,number from -10 to 10}
        myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Ghost[counter]} + {0,1,0} time 1.5
        set myEffect color red 148 green 0 blue 211
        SCALE of myEffect = 0.1
        add villager Ghost[counter] to platoon whichPlatoon
        disable Ghost[counter] hurt by fire
        disable Ghost[counter] set on fire
        run background script LC14_GhostScript(Ghost[counter],whichMesh)

        //Set new mesh
        if whichMesh == s_aztecwarrior_ghost
            set anim speed of Ghost[counter] to 0.75
            override mesh for Ghost[counter] with "..\skins\s_aztecwarrior_ghost"
            set Ghost[counter] carrying CARRIED_OBJECT_NORSE_SWORD
            disable Ghost[counter] can be fisted
        elsif whichMesh == s_skeletonadultland2
            set anim speed of Ghost[counter] to 0.7
            override mesh for Ghost[counter] with "..\skins\s_skeletonadultland2"
            set Ghost[counter] carrying CARRIED_OBJECT_NORSE_SWORD
        else
            set anim speed of Ghost[counter] to 0.7
            override mesh for Ghost[counter] with "..\skins\s_undeadwarrior_noghost"
            set Ghost[counter] carrying CARRIED_OBJECT_AZTEC_CLUB
        end if
        counter++
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Villager} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    delete Villager

end script LC14_CreateUndeadPlatoon


begin script LC14_GhostScript(myGhost,whichMesh)

    myEffect = 0
    PlayerAlignment = 0

start

    while myGhost exists and HEALTH of myGhost > 0.00
        wait until HEALTH of myGhost == 0 or 5 seconds
        if whichMesh == s_aztecwarrior_ghost
            //set anim speed of myGhost to 0.75
            override mesh for myGhost with "..\skins\s_aztecwarrior_ghost"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
            disable myGhost can be fisted
        elsif whichMesh == s_skeletonadultland2
            //set anim speed of myGhost to 0.5
            override mesh for myGhost with "..\skins\s_skeletonadultland2"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
        else
            //set anim speed of myGhost to 0.6
            override mesh for myGhost with "..\skins\s_undeadwarrior_noghost"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_AZTEC_CLUB
        end if
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {myGhost} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    HEALTH of myGhost = 0.00

    while myGhost exists
        wait until HEALTH of myGhost == 0 or 5 seconds
        if whichMesh == s_aztecwarrior_ghost
            //set anim speed of myGhost to 0.75
            set myGhost carrying CARRIED_OBJECT_NONE
            override mesh for myGhost with "..\skins\s_aztecwarrior_ghost"
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
            disable myGhost can be fisted
        elsif whichMesh == s_skeletonadultland2
            //set anim speed of myGhost to 0.5
            override mesh for myGhost with "..\skins\s_skeletonadultland2"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
        else
            //set anim speed of myGhost to 0.6
            override mesh for myGhost with "..\skins\s_undeadwarrior_noghost"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_AZTEC_CLUB
        end if
    end while

    PlayerAlignment = get player 0 alignment
    PlayerAlignment = PlayerAlignment + 0.00040
    set player 0 alignment PlayerAlignment

end script LC14_GhostScript


begin script LC14_AIAltarCounter(whichBuilding)

    AltarCount = 0

start

begin loop
    if HEALTH of whichBuilding == 1.0 and PLAYER of whichBuilding == 1 and AltarCount == 0
        AltarCount = 1
        LC14_AltarCounter += 1
    elsif AltarCount == 1
        if HEALTH of whichBuilding < 1.0 or PLAYER of whichBuilding != 1 or whichBuilding not exists
            AltarCount = 0
            LC14_AltarCounter -= 1
        end if
    end if
    until whichBuilding not exists and AltarCount == 0
end loop

end script LC14_AIAltarCounter