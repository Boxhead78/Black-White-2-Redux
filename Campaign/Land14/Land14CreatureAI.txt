///////////////////////////////////////////////////////////////////////////////
//    Land 14 Creature AI
//    ~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
//    Globals
//-----------------------------------------------------------------------------

define script LC14_AICreature
define script LC14_CreatureLogic

global CreatureAttackPos[3]
global CreatureBuildPos[2]
global CreatureGathererPos[3]

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

begin script LC14_AICreature

    CreaturePos = marker at {1719.73,113.45,1772.07}
    PlayerCreature = 0
    PlayerCreatureType = 0

start

    CreatureAttackPos[0] = marker at {1105.73,121.39,1961.69}//AztecCapital

    CreatureGathererPos[0] = marker at {1991.19,101.86,902.51}//AztecCapital
    CreatureGathererPos[1] = marker at {1643.38,126.45,1805.61}//AztecTown1
    CreatureGathererPos[2] = marker at {958.86,56.72,1208.12}//AztecTown2

    PlayerCreatureType = persistent data "PLAYER_CREATURE_TYPE"
    if PlayerCreatureType == variable CREATURE_TYPE_LION
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_LION at {CreaturePos}

    elsif PlayerCreatureType == variable CREATURE_TYPE_APE 
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_APE at {CreaturePos}

    elsif PlayerCreatureType == variable CREATURE_TYPE_GORILLA
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_GORILLA at {CreaturePos}

    elsif PlayerCreatureType == variable CREATURE_TYPE_WOLF
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_WOLF at {CreaturePos}

    elsif PlayerCreatureType == variable CREATURE_TYPE_COW
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_COW at {CreaturePos}

    elsif PlayerCreatureType == variable CREATURE_TYPE_TURTLE
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_TURTLE at {CreaturePos}

    elsif PlayerCreatureType == variable CREATURE_TYPE_ZOMBIE_APE
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_ZOMBIE_APE at {CreaturePos}

    else// PlayerCreature == variable CREATURE_TYPE_TIGER
        LC14_EnemyCreature = create CREATURE CREATURE_TYPE_TIGER at {CreaturePos}
        
    end if

    ALIGNMENT of LC14_EnemyCreature = -1.0
    set creature LC14_EnemyCreature happiness to maximum
    
    teach LC14_EnemyCreature all
    
    set player 1 creature to LC14_EnemyCreature
    
    set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_HEAL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_BUILDER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_GATHERER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_SOLDIER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

    FireballAvailable[1] = 1
    LightningAvailable[1] = 1
    FireballMedRangeAvailable[1] = 1
    FireballHighRangeAvailable[1] = 1

    set creature LC14_EnemyCreature CREATURE_SIZE 1.0
    CREATURE_STRENGTH of LC14_EnemyCreature = 1.0
    ALIGNMENT of LC14_EnemyCreature = -0.85
    CREATURE_FATNESS of LC14_EnemyCreature = 0.5

    disable LC14_EnemyCreature action availability LA_EAT with LO_PLAYER_VILLAGER
    disable LC14_EnemyCreature action availability LA_EAT with LO_PLAYER_PLATOON
    disable LC14_EnemyCreature action availability LA_EAT with LO_ROCKS
    disable LC14_EnemyCreature action availability LA_EAT with LO_TREES
    disable LC14_EnemyCreature action availability LA_EAT with LO_TOYS
    disable LC14_EnemyCreature action availability LA_EAT with LO_POO
    disable LC14_EnemyCreature action availability LA_EAT with LO_SPELL_ORBS
    disable LC14_EnemyCreature action availability LA_POO with LO_PLAYER_VILLAGER
    disable LC14_EnemyCreature action availability LA_POO with LO_PLAYER_PLATOON
    disable LC14_EnemyCreature action availability LA_POO with LO_PLAYER_DEFENCES
    disable LC14_EnemyCreature action availability LA_POO with LO_PLAYER_STORAGE_PIT
    disable LC14_EnemyCreature action availability LA_POO with LO_PLAYER_BUILDING
    disable LC14_EnemyCreature action availability LA_PLAY
    disable LC14_EnemyCreature action availability LA_WATER with LO_PLAYER_VILLAGER
    disable LC14_EnemyCreature action availability LA_WATER with LO_PLAYER_PLATOON
    disable LC14_EnemyCreature action availability LA_HEAL with LO_OPPONENT_PLATOON
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_CREATURE_PEN
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_ROCKS
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_MINE
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_ORE_ROCKS
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_TREES
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_FIELDS
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_POO
    disable LC14_EnemyCreature action availability LA_ATTACK with LO_SPELL_ORBS

    release LC14_EnemyCreature

    run background script LC14_CreatureLogic

    wait 45 seconds

end script LC14_AICreature


begin script LC14_CreatureLogic

    Number = 0
    AITown[17]
    AICounter = -1
    CreatureRollRoleTimer = create timer for 0 seconds
    Position = 0

start

begin loop

    if AICounter > 16
        AICounter = -1
    else
        AITown[AICounter] = get town with id AICounter
        if get building ABODE_NUMBER_TOWN_CENTRE in AITown[AICounter] min built 1.0 not exists
            AITown[AICounter] = 0
        end if
    end if

    if HEALTH of LC14_EnemyCreature == 0.00
        wait until HEALTH of LC14_EnemyCreature > 0.00
        set creature LC14_EnemyCreature role to CR_NONE radius 500 at {AztecCapital}
        HEALTH of LC14_EnemyCreature = 1.0
    end if
    
    if town AITown[AICounter] is under takeover from player 0 and PLAYER of AITown[AICounter] == 1 and HEALTH of LC14_EnemyCreature > 0
        HEALTH of LC14_EnemyCreature = 1.0
        set creature LC14_EnemyCreature role to CR_SOLDIER radius 100 at {AITown[AICounter]}
        
        wait until HEALTH of LC14_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds

    elsif PLAYER of AITown[AICounter] == 1 and get distance from {AITown[AICounter]} to {MyCreature} < 300 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00 and HEALTH of LC14_EnemyCreature > 0
        HEALTH of LC14_EnemyCreature = 1.0
        while HEALTH of LC14_EnemyCreature > 0.00 and get distance from {AITown[AICounter]} to {MyCreature} <= 400 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00
            wait 15 seconds
            set creature LC14_EnemyCreature role to CR_SOLDIER radius 33 at {MyCreature}
        end while

    elsif EarlyAttackWaveAllowed == 1 and HEALTH of LC14_EnemyCreature > 0
        if LC14_Platoon[20] exists or LC14_Platoon[21] exists
            HEALTH of LC14_EnemyCreature = 1.0
            begin loop
                if LC14_Platoon[20] exists
                    set creature LC14_EnemyCreature role to CR_SOLDIER radius 75 at {LC14_Platoon[20]}
                elsif LC14_Platoon[21] exists
                    set creature LC14_EnemyCreature role to CR_SOLDIER radius 75 at {LC14_Platoon[21]}
                end if
                until LC14_Platoon[20] not exists and LC14_Platoon[21] not exists or 720 seconds or HEALTH of LC14_EnemyCreature == 0.00
            end loop
            set creature LC14_EnemyCreature role to CR_SOLDIER radius 75 at {PlayerTown}
            wait until HEALTH of LC14_EnemyCreature == 0.00 or 360 seconds
            EarlyAttackWaveAllowed = 0
        end if

    elsif LC14_Platoon[37] exists and LC14_Platoon[38] exists and LC14_Platoon[39] exists
        HEALTH of LC14_EnemyCreature = 1.0
        begin loop
            if LC14_Platoon[37] exists
                set creature LC14_EnemyCreature role to CR_SOLDIER radius 75 at {LC14_Platoon[37]}
            elsif LC14_Platoon[38] exists
                set creature LC14_EnemyCreature role to CR_SOLDIER radius 75 at {LC14_Platoon[38]}
            elsif LC14_Platoon[39] exists
                set creature LC14_EnemyCreature role to CR_SOLDIER radius 75 at {LC14_Platoon[39]}
            end if
            until LC14_Platoon[37] not exists and LC14_Platoon[38] not exists and LC14_Platoon[39] not exists or 720 seconds or HEALTH of LC14_EnemyCreature == 0.00
        end loop
        set creature LC14_EnemyCreature role to CR_SOLDIER radius 75 at {CreatureAttackPos[0]}
        wait until HEALTH of LC14_EnemyCreature == 0.00 or 360 seconds

    elsif HEALTH of LC14_EnemyCreature > 0.00 and get CreatureRollRoleTimer time remaining <= 0
        if PLAYER of AITown[AICounter] == 1
            Position = marker at {AITown[AICounter]} + {number from 50 to -50,number from 50 to -50}
            if number from 1 to 3 == 1
                set creature LC14_EnemyCreature role to CR_GATHERER radius 200 at {Position}
            else
                set creature LC14_EnemyCreature role to CR_NONE radius 200 at {Position}
            end if
            CreatureRollRoleTimer = create timer for 360 seconds
        
        end if

    end if

    AICounter += number from 0 to 16
    until LandWon == 1
end loop

//wait until HEALTH of LC14_EnemyCreature == 0.00
wait until get distance from camera position to {LC14_EnemyCreature} < 500 and not {LC14_EnemyCreature} viewed
delete LC14_EnemyCreature

end script LC14_CreatureLogic