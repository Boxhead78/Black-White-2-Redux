///////////////////////////////////////////////////////////////////////////////
//           Undead Armies Script
//    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

define script UA_GraveyardCheck
define script UA_SpawnUndeadPlatoonCheck(Graveyard,Flag,Foundation)
define script UA_CreateUndeadPlatoon(Graveyard,howManyVillagers)

global UA_myUsedUndead = 0

begin script UA_GraveyardCheck

    FlagObject = 0
    myCounter = 0
    myGraveyard = 0
    myFlag = 0
    myFoundation = 0
    myTown[22]
    Counter = 0

start

begin loop
    if get player 0 alignment < -0.75
        if Counter > 20
            Counter = 0
        end if
        
        myTown[Counter] = get town with id Counter
        if myTown[Counter] exists and PLAYER of myTown[Counter] == 0
            myGraveyard = get random abode of type ABODE_NUMBER_GRAVEYARD in town myTown[Counter]
            FlagObject = get FEATURE FEATURE_INFO_SEVEN_SAMURAI_BASE at {myGraveyard} radius 10
            if HEALTH of myGraveyard >= 1.0 and FlagObject not exists
                myFlag = create MOBILE_OBJECT ARMY_FLAG_INFO_GREEK_MELEE at {myGraveyard}
                override mesh for myFlag with "..\models\m_bannerundead"
                ALTITUDE of myFlag = 1.0
                ANGLE of myFlag = ANGLE of myGraveyard + 180
                SCALE of myFlag = 3.0
                disable myFlag pickup
                disable myFlag moveable
                enable myFlag indestructible
                myFoundation = create FEATURE FEATURE_INFO_SEVEN_SAMURAI_BASE at {myGraveyard}
                SCALE of myFoundation = 0.01
                disable myFoundation pickup
                disable myFoundation moveable
                enable myFoundation indestructible
                run background script UA_SpawnUndeadPlatoonCheck(myGraveyard,myFlag,myFoundation)
            end if
        end if
        Counter++
    else
        wait 5 seconds
    end if
end loop

end script UA_GraveyardCheck


begin script UA_SpawnUndeadPlatoonCheck(Graveyard,Flag,Foundation)

    myArmySize = 0
    myDeadPeople = 0
    myTempDeath = 0
    Tooltip[2]

start

while HEALTH of Graveyard >= 1.0 and Graveyard exists and Flag exists and get player 0 alignment < -0.75
    myDeadPeople = get total of stat STATS_EVT_SACRIFICES
    myTempDeath = myDeadPeople - UA_myUsedUndead
    if myTempDeath >= 30
        if hand position near {Flag} radius 4 and Tooltip[0] not exists
            Tooltip[0] = create tooltip on Flag with text "HELP_TEXT_PAUSE_STATS_95"
        end if
    else
        if hand position near {Flag} radius 4 and Tooltip[1] not exists
            Tooltip[1] = create tooltip on Flag with text "HELP_TEXT_PAUSE_STATS_111"
        end if
    end if
    if hand position not near {Flag} radius 4 + 1 and Tooltip[0] exists
        delete Tooltip[0]
    end if
    if hand position not near {Flag} radius 4 + 1 and Tooltip[1] exists
        delete Tooltip[1]
    end if

    if myTempDeath >= 30
        if myTempDeath >= 250
            myArmySize = 50
        elsif myTempDeath >= 200
            myArmySize = 40
        elsif myTempDeath >= 150
            myArmySize = 30
        elsif myTempDeath >= 100
            myArmySize = 20
        elsif myTempDeath >= 50
            myArmySize = 10
        end if
        if (bindable action BINDABLE_ACTION_TYPE_ACTION performed or bindable action BINDABLE_ACTION_TYPE_MOVE performed) and hand position near {Flag} radius 4
            run script UA_CreateUndeadPlatoon(Graveyard,myArmySize)
        end if
    end if
end while

delete Flag
delete Foundation

end script UA_SpawnUndeadPlatoonCheck


begin script UA_CreateUndeadPlatoon(Graveyard,howManyVillagers)

    ArmyFlag = 0
    ArmySize = 0
    Ghost[999]
    counter = 0
    maxTries = 0
    myEffect = 0
    myArmy = 0
    RelativePos = 0
    Villager = 0
    VillagerForNavigation = 0
    VillagerTemp = 0

start

    UA_myUsedUndead += howManyVillagers
    begin loop
        RelativePos = marker at get world position from Graveyard to {number from -10 to 10,number from -10 to 10}
        VillagerForNavigation = get living villager at {Graveyard} radius 250
        maxTries++
        until VillagerForNavigation can navigate to {RelativePos} or maxTries > 100
    end loop

    if maxTries > 100
        RelativePos = marker at get world position from Graveyard to {4,4}
    end if
    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {RelativePos} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {RelativePos} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 1.0

    myEffect = create visual effect VISUAL_MIRACLE_HEAL_GROUND at {RelativePos} + {0,2,0} time 20
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    wait 3.33 seconds
    myArmy = create platoon PLATOON_INFO_SKELETON_MELEE at {RelativePos} with howManyVillagers men and 0 women
    attach atmos "ghost_legion" to myArmy// range 30
    set myArmy atmos group 5 time 1.5 seconds
    PLAYER of myArmy = 0
    disable platoon myArmy merge
    disable platoon myArmy split
    disable platoon myArmy can disband
    disable platoon myArmy camp

    //set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_MASS of myArmy to 1.0
    set platoon movement value FORMATION_MOVEMENT_STAT_ACCELERATION_MAX of myArmy to 30.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MAX of myArmy to 9.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MIN of myArmy to 5.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_CHARGE_SPEED of myArmy to 10.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_FIGHTER_SPEED of myArmy to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_SPEED of myArmy to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_CHARGE_SPEED of myArmy to 10.0

    ArmyFlag = get SCRIPT_OBJECT_TYPE_ARMY_FLAG at {myArmy} radius 50
    override mesh for ArmyFlag with "..\models\m_bannerundead"
    disable platoon myArmy camp

end script UA_CreateUndeadPlatoon
