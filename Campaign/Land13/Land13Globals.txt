///////////////////////////////////////////////////////////////////////////////
//    Land 13 Globals
//    ~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

// Other Defines
define oCreature = 0
define script Globals
define script LC13_CreateUndeadPlatoon(whichPlatoon,howManyVillagersToDelete,howManyVillagersToSpawn,whichMesh)
define script LC13_GhostScript(myGhost,whichMesh)
define script LC13_AIAltarCounter(whichBuilding)
define script LC13_DynamicDifficulty

define s_aztecwarrior_ghost = 0
define s_skeletonadultland2 = 1
define s_undeadwarrior_noghost = 2

// Debug switch
global DEBUG = 0
global Intro = 1

global LC13_EnemyCreature = 0
global LC13_EnemyWolf = 0
global CinemaIsRunning = 0
global MigrationsAccepted = 0
global TownsConverted = 0
global TownsAggressiveConverted = 0
global TownsPeacefulConverted = 0
global UnderAttack = 0
global IntroFinished = 0
global DeleteWeather = 0
global AttackingPlatoons = 0
global GenericCommentTimer = 0
global UnitDeadTimer = 0
global AIVolcano = 0
global SacrificeAllowed = 0
global SkeletonsWave = 0
global SkeletonCatapultRecAllowed = 0
global LC13_NumberOfSkeletonPlatoons = 0
global VolcanoFired = 0
global EpicRunning = 0
global VolcanoStopFireBalls = 0
global VolcanoActive = 0
global TriedAztecTown1Attack = 0
global SendStonehengeTroops = 0
global LC13_AltarCounter = 0
global HardMode = 0
global LandWon = 0
global PlayerAlignmentFinal = 0
global StaticPlatoonsActionTimer = 0
global StaticPlatoonsActive = 0

global PlayerTown = 0
global MinorTown1 = 0
global MinorTown2 = 0
global MinorTown3 = 0
global MinorTown4 = 0
global MinorTown5 = 0
global MinorTown6 = 0
global MinorTown7 = 0
global MinorTown8 = 0
global AztecTown1 = 0
global AztecTown2 = 0
global AztecCapital = 0
global NearestSettlement = 0

global MINOR_TOWN1_IMPRESSIVENESS = 3000
global MINOR_TOWN2_IMPRESSIVENESS = 27000
global MINOR_TOWN3_IMPRESSIVENESS = 5000
global MINOR_TOWN4_IMPRESSIVENESS = 9000
global MINOR_TOWN5_IMPRESSIVENESS = 24000
global MINOR_TOWN6_IMPRESSIVENESS = 21000
global MINOR_TOWN7_IMPRESSIVENESS = 17000
global MINOR_TOWN8_IMPRESSIVENESS = 31000
global AZTECTOWN1_IMPRESSIVENESS = 36000
global AZTECTOWN2_IMPRESSIVENESS = 24000
global AZTECCAPITAL_IMPRESSIVENESS = 36500

global AttackNearestTownTimer = 0
global LC13_RecruitTimer1 = 0
global LC13_RecruitTimer2 = 0
global LC13_RecruitTimer3 = 0

global WolfVillagerTimer = 0

begin script Globals

SecondPlaythrough = 0

start

    WolfVillagerTimer = create timer for 999999 seconds

    GenericCommentTimer = create timer for 0 seconds
    UnitDeadTimer = create timer for 200 seconds

    MINOR_TOWN1_IMPRESSIVENESS = MINOR_TOWN1_IMPRESSIVENESS
    MINOR_TOWN2_IMPRESSIVENESS = MINOR_TOWN2_IMPRESSIVENESS
    MINOR_TOWN3_IMPRESSIVENESS = MINOR_TOWN3_IMPRESSIVENESS *  ImpressivenessModifier
    MINOR_TOWN4_IMPRESSIVENESS = MINOR_TOWN4_IMPRESSIVENESS *  ImpressivenessModifier
    AZTECTOWN1_IMPRESSIVENESS = AZTECTOWN1_IMPRESSIVENESS *  ImpressivenessModifier
    AZTECTOWN2_IMPRESSIVENESS = AZTECTOWN2_IMPRESSIVENESS *  ImpressivenessModifier
    AZTECCAPITAL_IMPRESSIVENESS = AZTECCAPITAL_IMPRESSIVENESS *  ImpressivenessModifier

    SecondPlaythrough = persistent data "SECOND_PLAYTHROUGH"
    HardMode = persistent data "HARD_MODE"

    if HardMode == 1
        AZTECTOWN1_IMPRESSIVENESS += 4000
        AZTECTOWN2_IMPRESSIVENESS += 4000
        AZTECCAPITAL_IMPRESSIVENESS += 4000
    end if

    if Difficulty >= 5
        AZTECTOWN1_IMPRESSIVENESS -= 2000
        AZTECTOWN2_IMPRESSIVENESS -= 2000
        AZTECCAPITAL_IMPRESSIVENESS += 4000
    end if

    GB_PlayerTown = get town with id 0//For Global Script
    PlayerTown = get town with id 0
    MinorTown1 = get town with id 3 //First Town
    MinorTown2 = get town with id 4 //Seventh Town
    MinorTown3 = get town with id 5 //Second Town
    MinorTown4 = get town with id 6 //Third Town
    MinorTown5 = get town with id 7 //Sixth Town
    MinorTown6 = get town with id 8 //Fifth Town
    MinorTown7 = get town with id 9 //Fourth Town
    MinorTown8 = get town with id 10 //Eigth Town
    AztecTown1 = get town with id 1
    AztecTown2 = get town with id 2
    AztecCapital = get town with id 11

    PlayerAlignmentFinal = get player 0 alignment
    StaticPlatoonsActionTimer = create timer for 1500 seconds
    ET_FireCheckTimer = create timer for 0 seconds

end script Globals


begin script LC13_CreateUndeadPlatoon(whichPlatoon,howManyVillagersToDelete,howManyVillagersToSpawn,whichMesh)

    ArmyFlag = 0
    ArmySize = 0
    Ghost[350]
    counter = 0
    myEffect = 0
    Villager = 0
    VillagerTemp = 0

start

    attach atmos "ghost_legion" to whichPlatoon range 40

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {whichPlatoon} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {whichPlatoon} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 1.0

    myEffect = create visual effect VISUAL_MIRACLE_HEAL_GROUND at {whichPlatoon} + {0,2,0} time 12
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    //Remove old villagers
    ArmySize = get number of villagers in platoon whichPlatoon
    force while get number of villagers in platoon whichPlatoon > 1
        Villager = get villager from platoon whichPlatoon nearest {whichPlatoon}
        delete Villager
    end while
    Villager = get villager from platoon whichPlatoon nearest {whichPlatoon}
    SCALE of Villager = 0.01

    //set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_MASS of whichPlatoon to 1.0
    set platoon movement value FORMATION_MOVEMENT_STAT_ACCELERATION_MAX of whichPlatoon to 30.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MAX of whichPlatoon to 9.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MIN of whichPlatoon to 5.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_CHARGE_SPEED of whichPlatoon to 10.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_FIGHTER_SPEED of whichPlatoon to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_SPEED of whichPlatoon to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_CHARGE_SPEED of whichPlatoon to 10.0

    ArmyFlag = get SCRIPT_OBJECT_TYPE_ARMY_FLAG at {whichPlatoon} radius 50
    override mesh for ArmyFlag with "..\models\m_bannerundead"
    disable platoon whichPlatoon camp

    while counter < howManyVillagersToSpawn and counter < ArmySize
        //Create ghosts
        Ghost[counter] = create VILLAGER AZTEC_FORESTER at {whichPlatoon} + {number from -10 to 10,number from -10 to 10}
        myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Ghost[counter]} + {0,1,0} time 1.5
        set myEffect color red 148 green 0 blue 211
        SCALE of myEffect = 0.1
        add villager Ghost[counter] to platoon whichPlatoon
        disable Ghost[counter] hurt by fire
        disable Ghost[counter] set on fire
        run background script LC13_GhostScript(Ghost[counter],whichMesh)
        counter++
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Villager} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    delete Villager

end script LC13_CreateUndeadPlatoon


begin script LC13_GhostScript(myGhost,whichMesh)

    myEffect = 0
    PlayerAlignment = 0

start

    while myGhost exists and HEALTH of myGhost > 0.00 and LandWon == 0 and PLAYER of myGhost == 1
        if whichMesh == s_aztecwarrior_ghost
            //set anim speed of myGhost to 0.75
            override mesh for myGhost with "..\skins\s_aztecwarrior_ghost"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
            disable myGhost can be fisted
        elsif whichMesh == s_skeletonadultland2
            //set anim speed of myGhost to 0.5
            override mesh for myGhost with "..\skins\s_skeletonadultland2"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
        else
            //set anim speed of myGhost to 0.6
            override mesh for myGhost with "..\skins\s_undeadwarrior_noghost"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_AZTEC_CLUB
        end if
        wait until HEALTH of myGhost == 0 or 5 seconds
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {myGhost} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    HEALTH of myGhost = 0.00

    begin loop
        if LandWon == 0
            if whichMesh == s_aztecwarrior_ghost
                //set anim speed of myGhost to 0.75
                override mesh for myGhost with "..\skins\s_aztecwarrior_ghost"
                set myGhost carrying CARRIED_OBJECT_NONE
                set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
                disable myGhost can be fisted
            elsif whichMesh == s_skeletonadultland2
                //set anim speed of myGhost to 0.5
                override mesh for myGhost with "..\skins\s_skeletonadultland2"
                set myGhost carrying CARRIED_OBJECT_NONE
                set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
            else
                //set anim speed of myGhost to 0.6
                override mesh for myGhost with "..\skins\s_undeadwarrior_noghost"
                set myGhost carrying CARRIED_OBJECT_NONE
                set myGhost carrying CARRIED_OBJECT_AZTEC_CLUB
            end if
        else
            delete myGhost
        end if
        wait until HEALTH of myGhost == 0 or 5 seconds
        until not myGhost exists
    end loop

end script LC13_GhostScript


begin script LC13_AIAltarCounter(whichBuilding)

    AltarCount = 0

start

begin loop
    if HEALTH of whichBuilding == 1.0 and PLAYER of whichBuilding == 1 and AltarCount == 0
        AltarCount = 1
        LC13_AltarCounter += 1
    elsif AltarCount == 1
        if HEALTH of whichBuilding < 1.0 or PLAYER of whichBuilding != 1 or whichBuilding not exists
            AltarCount = 0
            LC13_AltarCounter -= 1
        end if
    end if
    until whichBuilding not exists and AltarCount == 0
end loop

end script LC13_AIAltarCounter


begin script LC13_DynamicDifficulty

    Counter = 0
    DifficultyTimer = create timer for 3600 seconds

    RecruitSizeModifierTemp = 0
    RecruitModifierTemp = 0
    EpicModifierTemp = 0
    BuilderModifierTemp = 0

start

//Game gets constantly easier
begin loop
    wait 2.5 seconds
    if get DifficultyTimer time remaining <= 0
        RecruitModifier *= 1.15
        EpicModifier *= 1.25
        BuilderModifier *= 1.25
        DifficultyTimer = create timer for 1800 seconds
        Counter++
    end if
    until Counter >= 3
end loop

//Store old values
RecruitSizeModifierTemp = RecruitSizeModifier
RecruitModifierTemp = RecruitModifier
EpicModifierTemp = EpicModifier
BuilderModifierTemp = BuilderModifier

//Give the AI a comeback for 30 minutes
RecruitSizeModifier = 2.0
//RecruitModifier = 1.25
BuilderModifier = 1.25
EpicModifier = 1.0

wait 1800 seconds

//Now restore old values
RecruitSizeModifier = RecruitSizeModifierTemp
RecruitModifier = RecruitModifierTemp
EpicModifier = EpicModifierTemp
BuilderModifier = BuilderModifierTemp

end script LC13_DynamicDifficulty