///////////////////////////////////////////////////////////////////////////////
//    Land 13 Outro
//    ~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

define script LC13_Outro
define script LC13_Departure

begin script LC13_Outro

    AtmosMixer = create mixer
    PlayerAlignment = 0

start

wait until get player 1 town total == 0

PD_ForcedSaving = 1
wait until PD_ForcedSaving == 0 or 10 seconds
stop script "SavePersistentDataSetData"
set persistent data "PREVLAND_OVER" to 1
run script SavePersistentDataGetData
LandWon = 1

begin cinema

    //AGC: Moved fade out & wait inside the cinema block
    set fade red 0 green 0 blue 0 time 2
    wait 2 seconds
    set town PlayerTown capture all enemy towns
    disable dialogue manager
    disable game can be lost
    if PLAYER of AztecCapital == 0
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 700000 alignment -0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature - 0.10
    else
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 700000 alignment 0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature + 0.10
    end if
    set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND value 1
    stop script "LC13_Speeches"
    stop script "LC13_GenericTalusSpeeches"
    stop script "LC13_GateHouseManager"
    stop script "LC13_CreatureLogic"

    disable atmos sound
    //set atmos volume 0
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.0
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.0

    //24 seconds
    reset camera path
    queue camera move with position {1216.81,400.22,1810.26} focus {AztecCapital} + {0,-10,0} time 0.0
    queue camera move with position {1386.27,375.00,1761.20} focus {AztecCapital} + {0,-60,0} time 14.0
    queue camera move with position {1656.05,380.60,1618.18} focus {AztecCapital} + {0,-100,0} time 8.0
    play camera path with easein 20 easeout 8 method SCRIPT_CAMERAPATH_SMOOTHEST
    set fade in time 3

    wait 2.5 seconds

    if PLAYER of AztecCapital == 0
        say "BW2XT_LAND03_NEW_OUTRO_15"
        clear dialogue
        close dialogue
        wait BW2XT_LAND03_NEW_OUTRO_15 + 1 seconds
    else
        say "BW2XT_LAND03_NEW_OUTRO_10"
        clear dialogue
        close dialogue
        wait BW2XT_LAND03_NEW_OUTRO_10 + 1 seconds
    end if

    wait 0.5 seconds

    say "BW2XT_LAND03_NEW_OUTRO_20"
    clear dialogue
    close dialogue
    wait BW2XT_LAND03_NEW_OUTRO_20 + 1 seconds
    say "BW2XT_LAND03_NEW_OUTRO_30"
    clear dialogue
    close dialogue
    wait BW2XT_LAND03_NEW_OUTRO_30 + 1 seconds

    wait 1.5 seconds

    say "BW2XT_LAND03_NEW_OUTRO_40"
    clear dialogue
    close dialogue
    wait BW2XT_LAND03_NEW_OUTRO_40 + 1 seconds

    wait until camera ready

    //set atmos volume 0.45
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.45
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.45

    stop music with fadetime 5

end cinema

run background script LC13_Departure

end script LC13_Outro


begin script LC13_Departure

    DepartureScrollPos = marker at {1419.26,320.53,1744.70}
    DepartureScroll = 0
    Portal = 0
    
start

    WT_SkyTransitionGlobalInfluence = 1
    enable global influence

	vtx_max_armies = 5
	vtx_max_sieges = 3
	vtx_max_orerocks = 15
	vtx_tree_max = 30
	vtx_rock_max = 20
	vtx_ore_max = 20000
	vtx_food_max = 20000
	vtx_wood_max = 20000
	vtx_vill_max = 100
    vtx_max_arm_men = 150
    vtx_rock_max = 10
    vtx_size = 2.5
    vtx_radius = 30

    run script Vtx_Create_Exiting(1557.70,262.61,2152.95)
    
    wait until vtx_scroll_clicked == VTX_TRUE
    
    // Get the dialogue for the enemy and for the advisors
    
    set fade red 0 green 0 blue 0 time 2
    wait 2 seconds
    DM_DynamicMusicDisallowed = 1
    begin cinema
        start music "farming_communtiy_land2" loop 10
        wait 2 seconds
    
        queue camera move with position {1557.70,262.61,2152.95} + {10,7.5,10} focus {1557.70,262.61,2152.95} + {0,5,0} time 0.0
        queue camera move with position {1557.70,262.61,2152.95} + {7.5,5,7.5} focus {1557.70,262.61,2152.95} time 5.0
        queue camera move with position {1557.70,262.61,2152.95} + {1,1.5,1} focus {1557.70,262.61,2152.95} time 4.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE
        set fade in time 1
        wait 4 seconds
        set fade red 0 green 0 blue 0 time 3
        wait 3 seconds
        wait 1 seconds
    end cinema
    
    //TRIGGER processing of land end objectives
    process land end objectives
    wait until land end objectives processed    
    
    release MyCreature
    MyCreature = get player 0 creature
    if MyCreature exists
        save my_creature
    end if
    DM_DynamicMusicDisallowed = 0

    disable game can be lost
    run script LandExit

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds

    disable load screen
    load map "./Data/Landscape/BW2/Land14.bwe"
    wait 3 seconds

end script LC13_Departure
