///////////////////////////////////////////////////////////////////////////////
//    Land 10 Creature AI
//    ~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
//    Globals
//-----------------------------------------------------------------------------

define script LC10_AICreature
define script LC10_CreatureLogic

global CreatureAttackPos[3]
global CreatureBuildPos[2]
global CreatureGathererPos[3]

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

begin script LC10_AICreature

start

    CreatureAttackPos[0] = marker at {1367.80,97.36,1874.76}//PlayerTown
    CreatureAttackPos[1] = marker at {1166.68,95.64,1329.72}//AztecTown1
    CreatureAttackPos[2] = marker at {1277.17,167.71,817.35}//AztecTown2

    CreatureGathererPos[0] = marker at {1605.97,132.59,2239.98}//AztecCapital
    CreatureGathererPos[1] = marker at {1363.32,94.66,1807.37}//AztecTown1
    CreatureGathererPos[2] = marker at {1195.43,94.41,1299.59}//AztecTown2

    LC10_EnemyCreature = create SCRIPT_OBJECT_TYPE_CREATURE CREATURE_TYPE_GORILLA at {1655.73,131.93,2239.88}
    
    set creature LC10_EnemyCreature happiness to maximum
    
    set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_HEAL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_BUILDER_2 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_GATHERER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_SOLDIER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

    FireballAvailable[1] = 1
    LightningAvailable[1] = 1
    FireballMedRangeAvailable[1] = 1
    FireballHighRangeAvailable[1] = 1

    set creature LC10_EnemyCreature CREATURE_SIZE 8.0
    CREATURE_STRENGTH of LC10_EnemyCreature = 0.9
    CREATURE_ALIGNMENT of LC10_EnemyCreature = -0.85
    CREATURE_FATNESS of LC10_EnemyCreature = 0.5

    disable LC10_EnemyCreature action availability LA_EAT with LO_PLAYER_VILLAGER
    disable LC10_EnemyCreature action availability LA_EAT with LO_PLAYER_PLATOON
    disable LC10_EnemyCreature action availability LA_EAT with LO_ROCKS
    disable LC10_EnemyCreature action availability LA_EAT with LO_TREES
    disable LC10_EnemyCreature action availability LA_EAT with LO_TOYS
    disable LC10_EnemyCreature action availability LA_EAT with LO_POO
    disable LC10_EnemyCreature action availability LA_EAT with LO_SPELL_ORBS
    disable LC10_EnemyCreature action availability LA_POO with LO_PLAYER_VILLAGER
    disable LC10_EnemyCreature action availability LA_POO with LO_PLAYER_PLATOON
    disable LC10_EnemyCreature action availability LA_POO with LO_PLAYER_DEFENCES
    disable LC10_EnemyCreature action availability LA_POO with LO_PLAYER_STORAGE_PIT
    disable LC10_EnemyCreature action availability LA_POO with LO_PLAYER_BUILDING
    disable LC10_EnemyCreature action availability LA_PLAY
    disable LC10_EnemyCreature action availability LA_WATER with LO_PLAYER_VILLAGER
    disable LC10_EnemyCreature action availability LA_WATER with LO_PLAYER_PLATOON
    disable LC10_EnemyCreature action availability LA_HEAL with LO_OPPONENT_PLATOON
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_CREATURE_PEN
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_ROCKS
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_MINE
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_ORE_ROCKS
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_TREES
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_FIELDS
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_POO
    disable LC10_EnemyCreature action availability LA_ATTACK with LO_SPELL_ORBS

    teach LC10_EnemyCreature all
    
    set player 1 creature to LC10_EnemyCreature
    
    wait 50 seconds
    
    release LC10_EnemyCreature

    run background script LC10_CreatureLogic

end script LC10_AICreature

begin script LC10_CreatureLogic

    Number = 0
    AITown[17]
    AICounter = -1
    CreatureRollRoleTimer = create timer for 0 seconds
    Position = 0

start

begin loop

    if AICounter > 16
        AICounter = -1
    else
        AITown[AICounter] = get town with id AICounter
    end if

    if HEALTH of LC10_EnemyCreature == 0.00
        wait until HEALTH of LC10_EnemyCreature > 0.00
        set creature LC10_EnemyCreature role to CR_NONE radius 500 at {AztecCapital}
        HEALTH of LC10_EnemyCreature = 1.0
    end if
    
    if town AITown[AICounter] is under takeover from player 0 and PLAYER of AITown[AICounter] == 1 and HEALTH of LC10_EnemyCreature > 0
        HEALTH of LC10_EnemyCreature = 1.0
        set creature LC10_EnemyCreature role to CR_SOLDIER radius 100 at {AITown[AICounter]}
        
        wait until HEALTH of LC10_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds

    elsif PLAYER of AITown[AICounter] == 1 and get distance from {AITown[AICounter]} to {MyCreature} < 300 and HEALTH of MyCreature > 0.00 and HEALTH of LC10_EnemyCreature > 0
        HEALTH of LC10_EnemyCreature = 1.0
        while HEALTH of LC10_EnemyCreature > 0.00 and get distance from {AITown[AICounter]} to {MyCreature} <= 500 and HEALTH of MyCreature > 0.00
            wait 15 seconds
            set creature LC10_EnemyCreature role to CR_SOLDIER radius 33 at {MyCreature}
        end while

        wait until HEALTH of LC10_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds

    elsif LC10_Platoon[70] exists and LC10_Platoon[71] exists and LC10_Platoon[72] exists and LC10_Platoon[73] exists
        if HEALTH of LC10_EnemyCreature > 0.00 and PrisonDestroyed == 2
            HEALTH of LC10_EnemyCreature = 1.0
            begin loop
                if LC10_Platoon[70] exists
                    set creature LC10_EnemyCreature role to CR_SOLDIER radius 75 at {LC10_Platoon[70]}
                elsif LC10_Platoon[71] exists
                    set creature LC10_EnemyCreature role to CR_SOLDIER radius 75 at {LC10_Platoon[71]}
                elsif LC10_Platoon[72] exists
                    set creature LC10_EnemyCreature role to CR_SOLDIER radius 75 at {LC10_Platoon[72]}
                elsif LC10_Platoon[73] exists
                    set creature LC10_EnemyCreature role to CR_SOLDIER radius 75 at {LC10_Platoon[73]}
                end if
                until (LC10_Platoon[70] not exists and LC10_Platoon[71] not exists and LC10_Platoon[72] not exists and LC10_Platoon[73] not exists) or 720 seconds or HEALTH of LC10_EnemyCreature == 0.00
            end loop
            set creature LC10_EnemyCreature role to CR_SOLDIER radius 300 at {CreatureAttackPos[0]}
            wait until HEALTH of LC10_EnemyCreature == 0.00 or 360 seconds
        end if
        
    end if

    AICounter += number from 0 to 16
    until LandWon == 1
end loop

//wait until HEALTH of LC10_EnemyCreature == 0.00
wait until get distance from camera position to {LC10_EnemyCreature} < 500 and not {LC10_EnemyCreature} viewed
delete LC10_EnemyCreature

end script LC10_CreatureLogic
