///////////////////////////////////////////////////////////////////////////////
//    Land 10 Volcano
//    ~~~~~~~~~~~~~~~
//
//    Start date:     06.05.18            By: Boxhead
//    Update date: 06.05.18            By: Boxhead
///////////////////////////////////////////////////////////////////////////////

define script LC10_Volcano

run script LC10_Volcano

begin script LC10_Volcano

    Volcano = 0
    TerminalVolcanoObject = 0
    VolcanoFirstTime = 1

start

    Volcano = create volcano at {1648.50,292.56,1371.04} player 2

    invoke Volcano EPIC_START_RISING
    wait 10 seconds

    begin loop

        attach atmos "spells" to Volcano range 750
        TerminalVolcanoObject = create terminal object at {Volcano} with duration of 540 seconds
        attach atmos "spells" to TerminalVolcanoObject// range 400
        invoke Volcano EPIC_ADD_NAVIGATION
        invoke Volcano EPIC_START_PLUME
        invoke Volcano EPIC_START_SMOKE
        if VolcanoFirstTime == 0
            wait 60 seconds or LC10_LandDone == 1
        end if

        invoke Volcano EPIC_START_EXPLOSION
        invoke Volcano EPIC_FLASH
        invoke Volcano EPIC_START_LAVA
        invoke Volcano EPIC_START_LAVA_EXPLOSION
        invoke Volcano EPIC_START_ROCK_SHOWER    
        invoke Volcano EPIC_START_BUBBLING_LAVA        
        invoke Volcano EPIC_START_EXPLOSION
        if IntroFinished == 1
            play string sound "SCRIPT1_VEPIC_EXPLOSION3"
            play string sound "SCRIPT1_VEPIC_VOLCANOSHOCKWAVHIGHPITCHED"
        end if

        VolcanoActive = 1
        wait 1240 seconds or LC10_LandDone == 1
        //Turn off Volcano for some time

        invoke Volcano EPIC_STOP_EXPLOSION
        invoke Volcano EPIC_STOP_PLUME
        invoke Volcano EPIC_STOP_LAVA
        invoke Volcano EPIC_STOP_BUBBLING_LAVA
        invoke Volcano EPIC_END_ROCK_SHOWER

        wait 500 seconds or LC10_LandDone == 1

        invoke Volcano EPIC_STOP_SMOKE
        invoke Volcano EPIC_STOP_ASH
        VolcanoActive = 0
        wait 900 seconds or LC10_LandDone == 1

        VolcanoFirstTime = 0

        until LC10_LandDone == 1
    end loop

    invoke Volcano EPIC_START_FALLING
        
    wait 60 seconds

    invoke Volcano EPIC_END_SPELL

end script LC10_Volcano