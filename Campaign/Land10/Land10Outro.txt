///////////////////////////////////////////////////////////////////////////////
//    Land 10 Outro
//    ~~~~~~~~~~~~~
//
//    Start date:     18.05.18        By:    Boxhead
//    Update date:    24.05.18        By: Boxhead
///////////////////////////////////////////////////////////////////////////////
define script LC10_Outro
define script LC10_Departure

begin script LC10_Outro

L10AztecTownConversionMethod = 0
PlayerAlignment = 0

start

wait until PLAYER of AztecCapital != 1 and PLAYER of AztecTown1 != 1 and PLAYER of AztecTown2 != 1 and PLAYER of PlayerTown != 1

PD_ForcedSaving = 1
wait until PD_ForcedSaving == 0 or 10 seconds
stop script "SavePersistentDataSetData"
set persistent data "PREVLAND_OVER" to 1
run script SavePersistentDataGetData

begin cinema

    //AGC: Moved fade out & wait inside the cinema block
    set fade red 0 green 0 blue 0 time 2
    wait 2 seconds
    LC10_LandDone = 1
    run background script LC10_Departure
    set town PlayerTown capture all enemy towns
    disable dialogue manager
    disable game can be lost
    set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND value 1
    //increment tribute by 250000
    PlayerAlignment = get player 0 alignment
    if PLAYER of AztecCapital == 0
        PlayerAlignment = PlayerAlignment - 0.05
        ALIGNMENT of MyCreature = ALIGNMENT of MyCreature - 0.1
    else
        PlayerAlignment = PlayerAlignment + 0.05
        ALIGNMENT of MyCreature = ALIGNMENT of MyCreature + 0.1
    end if
    set player 0 alignment PlayerAlignment
    stop script "LC10_Speeches"
    stop script "LC10_GateHouseManager"
    stop script "LC10_CreatureLogic"

    L10AztecTownConversionMethod = variable get town AztecCapital method of last conversion
    
    disable atmos sound
    set atmos volume 0

    //43 seconds
    reset camera path
    queue camera move with position {1651.48,160.00,2246.28} focus {AztecCapital} time 0.0
    queue camera move with position {1377.16,260.00,2245.71} focus {AztecCapital} + {0,40,0} time 37.0
    queue camera move with position {1501.66,250.00,2314.60} focus {1531.56,201.22,2366.84} time 6.0
    play camera path with easein 20 easeout 8 method SCRIPT_CAMERAPATH_EQUALDISTANCE
    set fade in time 1

    if L10AztecTownConversionMethod == 0
        L10AztecTownConversionMethod = number from 1 to 2
    end if

    if L10AztecTownConversionMethod == 1 //aggressive

        say "BW2T_SCRIPT_10FINAL_OUTRO_EVIL_10"
        //    AZTEC_GENERAL    How can this be? I am brought low by these Greeks! They call upon the divine energies for assistance. They fight in the shadow of some mighty god!
        wait until read
        
    elsif L10AztecTownConversionMethod == 2

        say "BW2T_SCRIPT_10FINAL_OUTRO_GOOD_10"
        //    AZTEC_GENERAL    These Greeks have built so mighty an empire upon this land. I vow to gather my forces and wreak terrible vengeance on them all!
        wait until read
        
    end if
        
    wait 2 seconds

    say "BW2T_SCRIPT_09FINAL_PDM_ENDLAND_30"
    wait until read

    say "BW2T_SCRIPT_09FINAL_PDM_ENDLAND_40"
    wait until read
        
    say "BW2T_SCRIPT_10FINAL_OUTRO_PICKUP85"
    //    AZTEC_GENERAL    Ensure that the Epic Miracle is charged. I intend to make good use of it!
    wait until read

    eject evil spirit
    eject good spirit
        
    say "BW2T_SCRIPT_10FINAL_OUTRO_PICKUP86"
    //    EVIL_ADVISOR    We have to follow them and end this. We got no choice.
    wait until read

    wait until camera ready
    enable atmos sound
    set atmos volume 0.45

    send evil spirit home
    send good spirit home

end cinema

end script LC10_Outro

begin script LC10_Departure

    DepartureScrollPos = marker at {1531.56,201.22,2366.84}
    DepartureScroll = 0
    Portal = 0
    
start

    enable global influence

    Portal = create visual effect VISUAL_EFFECT_TELEPORTER_HOOP at {DepartureScrollPos} + {0, 1.5, 0} time -1

    DepartureScroll = create highlight GOLD name "BW2T_SCRIPT_GENERIC_DEPART_LAND" remind "BW2T_SCRIPT_GENERIC_DEPART_LAND" at {DepartureScrollPos}
    ALTITUDE of DepartureScroll = 3

    wait until DepartureScroll right clicked

    begin dialogue
        eject good spirit
        //say "Are you sure you wish to leave the land?"
        say "BW2T_SCRIPT_03FINAL_ADVISORS_TUT_934"
        wait until read
        //say "Click again to confirm"
        say "BW2T_SCRIPT_03FINAL_ADVISORS_TUT_933"
        wait until read
        send good spirit home
    end dialogue
    
    clear right clicked object
    wait until DepartureScroll right clicked
    delete DepartureScroll
    
    // Get the dialogue for the enemy and for the advisors
    
    set fade red 0 green 0 blue 0 time 2
    wait 2 seconds
    begin cinema
        start music "farming_communtiy_land2" loop 10
        wait 2 seconds
    
        queue camera move with position {1531.56,201.22,2366.84} + {10,7.5,10} focus {1531.56,201.22,2366.84} + {0,5,0} time 0.0
        queue camera move with position {1531.56,201.22,2366.84} + {7.5,5,7.5} focus {1531.56,201.22,2366.84} time 5.0
        queue camera move with position {1531.56,201.22,2366.84} + {1,1.5,1} focus {1531.56,201.22,2366.84} time 4.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE
        set fade in time 1
        wait 4 seconds
        set fade red 0 green 0 blue 0 time 3
        wait 3 seconds
        wait 1 seconds
    end cinema

    if L10ShowingIslandNymphObjective == 0
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_9 with amount 1 text "BW2T_SCRIPT_09FINAL_CHALLENGENAME_ISLAND_OF_NYMPHS" amount 1 description "BW2T_SCRIPT_09FINAL_CHALLENGE_NYMPHS_REMINDER" start value 0 reward 40000
    end if

    if L10ShowingRainDanceObjective == 0
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_8 with amount 1 text "BW2T_SCRIPT_09FINAL_CHALLENGE_RAINDANCE_MISC_10" amount 1 description "BW2T_SCRIPT_09FINAL_CHALLENGE_RAINDANCE_MISC_20" start value 0 reward 40000
    end if
    
    ////
    //TRIGGER processing of land end objectives
    process land end objectives
    wait until land end objectives processed    
    
    release MyCreature
    MyCreature = get player 0 creature
    if MyCreature exists
        save my_creature
    end if

    disable game can be lost
    run script LandExit

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds

    load map "./Data/Landscape/BW2/Land11.bwe"
    wait 3 seconds

end script LC10_Departure