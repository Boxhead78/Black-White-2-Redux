///////////////////////////////////////////////////////////////////////////////
//    Land 10 Outro
//    ~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////
define script LC10_Outro
define script LC10_Departure

begin script LC10_Outro

AtmosMixer = create mixer
L10AztecTownConversionMethod = 0
PlayerAlignment = 0

start

wait until get player 1 town total == 0
LandWon = 1

PD_ForcedSaving = 1
wait until PD_ForcedSaving == 0 or 10 seconds
stop script "SavePersistentDataSetData"
set persistent data "PREVLAND_OVER" to 1
run script SavePersistentDataGetData

begin cinema

    //AGC: Moved fade out & wait inside the cinema block
    set fade red 0 green 0 blue 0 time 2
    wait 2 seconds
    LC10_LandDone = 1
    set town PlayerTown capture all enemy towns
    disable dialogue manager
    disable game can be lost
    //increment tribute by 250000
    if PLAYER of AztecCapital == 0
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 400000 alignment -0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature - 0.10
    else
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 400000 alignment 0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature + 0.10
    end if
    set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND value 1
    stop script "LC10_Speeches"
    stop script "LC10_GateHouseManager"
    stop script "LC10_CreatureLogic"

    L10AztecTownConversionMethod = variable get town AztecCapital method of last conversion
    
    disable atmos sound
    //set atmos volume 0.45
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.45
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.45

    //43 seconds
    reset camera path
    queue camera move with position {1651.48,160.00,2246.28} focus {AztecCapital} time 0.0
    queue camera move with position {1377.16,260.00,2245.71} focus {AztecCapital} + {0,40,0} time 42.0
    play camera path with easein 20 easeout 8 method SCRIPT_CAMERAPATH_EQUALDISTANCE
    set fade in time 1

    if L10AztecTownConversionMethod == 0
        L10AztecTownConversionMethod = number from 1 to 2
    end if

    if L10AztecTownConversionMethod == 1 //aggressive

        say "BW2T_SCRIPT_10FINAL_OUTRO_EVIL_10"
        //    AZTEC_GENERAL    How can this be? I am brought low by these Greeks! They call upon the divine energies for assistance. They fight in the shadow of some mighty god!
        wait until read
        
    elsif L10AztecTownConversionMethod == 2

        say "BW2T_SCRIPT_10FINAL_OUTRO_GOOD_10"
        //    AZTEC_GENERAL    These Greeks have built so mighty an empire upon this land. I vow to gather my forces and wreak terrible vengeance on them all!
        wait until read
        
    end if
        
    wait 2 seconds

    say "BW2T_SCRIPT_09FINAL_PDM_ENDLAND_30"
    wait until read

    say "BW2T_SCRIPT_09FINAL_PDM_ENDLAND_40"
    wait until read
        
    say "BW2T_SCRIPT_10FINAL_OUTRO_PICKUP85"
    //    AZTEC_GENERAL    Ensure that the Epic Miracle is charged. I intend to make good use of it!
    wait until read

    eject evil spirit
    eject good spirit
        
    say "BW2T_SCRIPT_10FINAL_OUTRO_PICKUP86"
    //    EVIL_ADVISOR    We have to follow them and end this. We got no choice.
    wait until read

    wait until camera ready
    enable atmos sound
    //set atmos volume 0
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.0
    set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.0

    send evil spirit home
    send good spirit home

end cinema

run background script LC10_Departure

end script LC10_Outro

begin script LC10_Departure
    
start

    WT_SkyTransitionGlobalInfluence = 1
    enable global influence

	vtx_max_armies = 5
	vtx_max_sieges = 2
	vtx_max_orerocks = 15
	vtx_tree_max = 30
	vtx_rock_max = 20
	vtx_ore_max = 20000
	vtx_food_max = 20000
	vtx_wood_max = 20000
	vtx_vill_max = 100
    vtx_max_arm_men = 75
    vtx_rock_max = 10
    vtx_size = 2.5
    vtx_radius = 30

    run script Vtx_Create_Exiting(1384.43,94.66,1620.16)
    
    wait until vtx_scroll_clicked == VTX_TRUE
    
    // Get the dialogue for the enemy and for the advisors
    
    set fade red 0 green 0 blue 0 time 2
    wait 2 seconds
    DM_DynamicMusicDisallowed = 1
    begin cinema
        start music "farming_communtiy_land2" loop 10
        wait 2 seconds
    
        queue camera move with position {1384.43,94.66,1620.16} + {10,7.5,10} focus {1384.43,94.66,1620.16} + {0,5,0} time 0.0
        queue camera move with position {1384.43,94.66,1620.16} + {7.5,5,7.5} focus {1384.43,94.66,1620.16} time 5.0
        queue camera move with position {1384.43,94.66,1620.16} + {1,1.5,1} focus {1384.43,94.66,1620.16} time 4.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE
        set fade in time 1
        wait 4 seconds
        set fade red 0 green 0 blue 0 time 3
        wait 3 seconds
        wait 1 seconds
    end cinema
    DM_DynamicMusicDisallowed = 0

    if L10ShowingIslandNymphObjective == 0
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_9 with amount 1 text "BW2T_SCRIPT_09FINAL_CHALLENGENAME_ISLAND_OF_NYMPHS" amount 1 description "BW2T_SCRIPT_09FINAL_CHALLENGE_NYMPHS_REMINDER" start value 0 reward 40000
    end if

    if L10ShowingRainDanceObjective == 0
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_8 with amount 1 text "BW2T_SCRIPT_09FINAL_CHALLENGE_RAINDANCE_MISC_10" amount 1 description "BW2T_SCRIPT_09FINAL_CHALLENGE_RAINDANCE_MISC_20" start value 0 reward 40000
    end if
    
    ////
    //TRIGGER processing of land end objectives
    process land end objectives
    wait until land end objectives processed    
    
    release MyCreature
    MyCreature = get player 0 creature
    if MyCreature exists
        save my_creature
    end if

    disable game can be lost
    run script LandExit

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds

    disable load screen
    load map "./Data/Landscape/BW2/Land11.bwe"
    wait 3 seconds

end script LC10_Departure