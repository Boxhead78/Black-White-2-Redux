///////////////////////////////////////////////////////////////////////////////
//    Land 4 Outro
//    ~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

define script LC4_Outro
define script LC4_EndLand
define script Land4Departure
define script Run_Land6

begin script LC4_Outro

    L4TownConversionMethod = 0
    GateHouse = 0
    Counter = 0
    CreationPos = 0
    Vagrants[100]
    CheckWhatconversionmethodwas=0
    PlayerAlignment = 0

start

    wait until get player 1 town total == 0
    LandWon = 1

    if DMM_IsRunning == 1
        DMM_StopMusic = 1
    end if

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds
    stop script "SavePersistentDataSetData"
    set persistent data "PREVLAND_OVER" to 1
    run script SavePersistentDataGetData

    set town PlayerTown capture all enemy towns
    disable dialogue manager
    disable game can be lost
    //increment tribute by 120000
    PlayerAlignment = get player 0 alignment
    if PLAYER of NorseCapital == 0
        L4TownConversionMethod = 1
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 120000 alignment -0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature - 0.10
    else
        L4TownConversionMethod = 2
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 120000 alignment 0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature + 0.10
    end if
    set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND value 1
    wait 2 seconds

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds
    stop script "SavePersistentDataSetData"
    if PLAYER of NorseCapital == 0
        set persistent data "LAND4METHODOFCONVERSION" to 1
    else
        set persistent data "LAND4METHODOFCONVERSION" to 2
    end if
    run script SavePersistentDataGetData

    run background script LC4_EndLand
    
    if L4TownConversionMethod == 1 //aggressive

    begin cinema
        //AGC: Moved fade out & wait inside the cinema block
        set fade red 0 green 0 blue 0 time 2
        wait 4 seconds
        
        reset camera path 
                   
        queue camera move with position {761.35,300.00,1093.60} focus {NorseCapital} time 0.0    
        queue camera move with position {874.89,280.00,1019.29} focus {923.51,240.00,1178.04} time 30.0
        queue camera move with position {932.11,250.00,1079.25} focus {1896.94,58.24,1298.12} time 12.0
        
        play camera path with easein 20 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE

        set fade in time 2
        wait 2 seconds
                
        say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_WAR_WIN_20"
        //My people bow low before the Greeks. 
        wait until read
        say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_WAR_WIN_30"
        //Their brutality knows no equal. Not even the Aztecs.
        wait until read
        say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_WAR_WIN_40"
        //We respect this power, this strength. But will the Japanese? And will our rulers, the Aztecs?
        wait until read
        say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_WAR_WIN_50"
        //I wonder if the old Prophesy is true? If the Greeks are the weak race brought to strength by a god?
        wait until read
        say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_WAR_WIN_60"
        //If so, will they fulfill the next chapter and go to Honshai, where their separated Greek brethren wait for them?
        wait until read
        
        eject good spirit
        eject evil spirit
        
        //say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_WAR_WIN_10"
        make good spirit play across 0.5 down 0.5 HELPDUDE_KM_04FINAL_PDM_ENDLAND_WAR_WIN_G_10
        make evil spirit play across 0.5 down 0.5 HELPDUDE_KM_04FINAL_PDM_ENDLAND_WAR_WIN_E_10
        
        wait until read
        
        
        send evil spirit home
        send good spirit home
        wait until camera ready
        wait 2 seconds
        end cinema

    elsif L4TownConversionMethod == 2 //impressiveness
        begin cinema
            //AGC: Moved fade out & wait inside the cinema block
            set fade red 0 green 0 blue 0 time 2
            wait 4 seconds
            
            force while Counter < 100
                CreationPos = marker at get target from {1901.00,59.33,1313.01} to {1941.46,58.78,1296.76} distance -Counter-20 angle 0
                CreationPos = marker at {CreationPos} + {number from -3 to 3, 0, number from -3 to 3}
                Vagrants[Counter] = create random villager of tribe TRIBE_TYPE_NORSE at {CreationPos}
                move Vagrants[Counter] position to {2046.34,49.55,1254.94}
                SPEED of Vagrants[Counter] = 0.1
                Counter++
            end while
            
            
            GateHouse = get HOUSE GATEHOUSE_F at {1947.52,58.91,1293.14} radius 100
            if GateHouse not exists
                GateHouse = get HOUSE GATEHOUSE at {1947.52,58.91,1293.14} radius 100
            end if
            set gate GateHouse open
            
            reset camera path 
            queue camera move with position {1799.82,125.00,1345.84} focus {1915.86,70.00,1301.96} time 0.0
            queue camera move with position {2079.80,95.00,1229.81} focus {2145.48,50.78,1159.56} time 30.0
            play camera path with easein 12 easeout 4 method SCRIPT_CAMERAPATH_EQUALTIME
            set fade in time 2
            //GEN:I have never seen such nobility in a tribe. The Greeks have shown a way other than war.        
            say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_IMPRESS_WIN_10"
            wait until read
            //GEN:I am inspired by their fairness and their civilisation.        
            say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_IMPRESS_WIN_20"
            wait until read
            //GEN:I shall consider allying with these Greeks. They have culture far beyond that of the Aztecs.        
            say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_IMPRESS_WIN_30"
            wait until read
            //GEN:And maybe the Prophesy is true. A returning god saving a weak tribe.        
            say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_IMPRESS_WIN_40"
            wait until read
            //GEN:It will be interesting to see how they fare against the strong Japanese. And the brutal Aztecs.        
            say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_IMPRESS_WIN_50"
            wait until read
            //GEN:I wonder if the Greeks will follow the Prophesy and go to Honshai, where some of their brethren survive?
            say "BW2T_SCRIPT_04FINAL_PDM_ENDLAND_IMPRESS_WIN_60"
            wait until read    
            eject good spirit
            //GA:Hurrah! Goodness and impressing has won the day!        
            make good spirit play across 0.5 down 0.5 HELPDUDE_KM_04FINAL_PDM_ENDLAND_IMPRESS_WIN_G_70
            wait until read
            send good spirit home
            
            
            reset camera path 
            queue camera move with position {2079.80,95.00,1229.81} focus {2145.48,50.78,1159.56} time 0.0
            queue camera move with position {2003.74,100.00,1281.56} focus {PlayerTown} time 12.0
            queue camera move with position {2049.84,105.00,1343.67} focus {2124.04,41.67,1364.62} time 6.0
            play camera path with easein 12 easeout 4 method SCRIPT_CAMERAPATH_EQUALTIME
            wait 18 seconds
            Counter = 0
            force while Counter < 100
                delete Vagrants[Counter] 
                Counter++
            end while
        end cinema

    end if

    run background script Land4Departure

end script LC4_Outro

begin script LC4_EndLand

start

    stop script "LC4_CreatureLogic"
    stop script "LC4_GatehouseManager"
    stop script "LC4_Speeches"

end script LC4_EndLand

begin script Land4Departure
    DepartureScrollPos = marker at {2124.04,41.67,1364.62}
    DepartureScroll = 0
    AreYouSureCommentSaid = 0
    Random = 0
    CommentTimer = create timer for 0 seconds
    TeleportEffect = 0
    
start

    WT_SkyTransitionGlobalInfluence = 1
    enable global influence
    
    vtx_max_armies = 5
    vtx_max_sieges = 2
    vtx_max_orerocks = 7
    vtx_tree_max = 12
    //vtx_rock_max = 20
    vtx_ore_max = 20000
    vtx_food_max = 20000
    vtx_wood_max = 20000
    vtx_vill_max = 100
    vtx_max_arm_men = 75
    vtx_rock_max = 5
    vtx_size = 2.5
    vtx_radius = 30

    run script Vtx_Create_Exiting(2124.04,41.67,1364.62)
    
    wait until vtx_scroll_clicked == VTX_TRUE

    L4Complete = 1
    
    run background script Run_Land6

end script Land4Departure

begin script Run_Land6

    CSMixer = 0

start

CSMixer = create mixer

    DM_DynamicMusicDisallowed = 1
    begin cinema
        start music "farming_communtiy_land2" loop 2
        set fade red 0 green 0 blue 0 time 2
        wait 2 seconds
        set camera position to {2089.77,50.00,1365.90}
        set camera focus to {2124.04,41.67,1364.62}        
        move camera position to {2115.96,50.00,1367.07} time 27
        move camera focus to {2124.04,41.67,1364.62} time 27
        set fade in time 2
        eject good spirit
        eject evil spirit
        say "BW2T_SCRIPT_04FINAL_ADVISORS_OUTRO_10"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_OUTRO_20"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_OUTRO_30"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_OUTRO_40"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_OUTRO_50"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_OUTRO_60"
        wait until read
        move camera position to {2126.85,44.00,1366.12} time 7
        move camera focus to {2124.04,40.67,1364.62} time 7
        send good spirit home
        send evil spirit home
        set fade red 0 green 0 blue 0 time 3
        set mixer CSMixer channel AUDIO_MIXER_CHANNEL_GLOBAL to 0.0 with fadetime 3
        wait 3.5 seconds
        wait 1 seconds
    end cinema
    DM_DynamicMusicDisallowed = 0

    release MyCreature
    MyCreature = get player 0 creature
    if MyCreature exists
        save my_creature
    end if

    if FistingBought == 1
        set research ARTEFACT_HAND_FISTING available to RESEARCH_AVAILABILITY_RESEARCHED
    end if

    //TRIGGER processing of land end objectives
    process land end objectives
    wait until land end objectives processed    
    
    disable game can be lost
    run script LandExit

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds

    disable load screen
    load map "./Data/Landscape/BW2/Land6.bwe"

    wait 3 seconds

end script Run_Land6
