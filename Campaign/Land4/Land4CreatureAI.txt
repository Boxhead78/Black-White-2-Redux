///////////////////////////////////////////////////////////////////////////////
//    Land 4 Creature AI
//    ~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
//    Globals
//-----------------------------------------------------------------------------

define script LC4_AICreature
define script LC4_CreatureLogic

global CreatureAttackPos[2]

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

begin script LC4_AICreature

start

    CreatureAttackPos[0] = marker at {1885.98,58.11,1293.16}//PlayerTown
    CreatureAttackPos[1] = marker at {2343.76,34.17,928.66}//PlayerTown

    LC4_EnemyCreature = create SCRIPT_OBJECT_TYPE_CREATURE CREATURE_TYPE_WOLF at {911.30,231.92,1132.32}
    
    set creature LC4_EnemyCreature happiness to maximum
    
    set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_BUILDER_1 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_GATHERER_2 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_SOLDIER_1 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

    set creature LC4_EnemyCreature CREATURE_SIZE 0.4
    CREATURE_STRENGTH of LC4_EnemyCreature = 0.4
    ALIGNMENT of LC4_EnemyCreature = 0.1
    CREATURE_FATNESS of LC4_EnemyCreature = 0.4

    if HardMode == 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_HEAL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_ROLE_BUILDER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_ROLE_GATHERER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_ROLE_SOLDIER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

        FireballAvailable[1] = 1
        LightningAvailable[1] = 1
        FireballMedRangeAvailable[1] = 1
        FireballHighRangeAvailable[1] = 1

        set creature LC4_EnemyCreature CREATURE_SIZE 1.0
        CREATURE_STRENGTH of LC4_EnemyCreature = 1.0
        CREATURE_FATNESS of LC4_EnemyCreature = 0.5
    end if

    disable LC4_EnemyCreature action availability LA_EAT with LO_PLAYER_VILLAGER
    disable LC4_EnemyCreature action availability LA_EAT with LO_PLAYER_PLATOON
    disable LC4_EnemyCreature action availability LA_EAT with LO_ROCKS
    disable LC4_EnemyCreature action availability LA_EAT with LO_TREES
    disable LC4_EnemyCreature action availability LA_EAT with LO_TOYS
    disable LC4_EnemyCreature action availability LA_EAT with LO_POO
    disable LC4_EnemyCreature action availability LA_EAT with LO_SPELL_ORBS
    disable LC4_EnemyCreature action availability LA_POO with LO_PLAYER_VILLAGER
    disable LC4_EnemyCreature action availability LA_POO with LO_PLAYER_PLATOON
    disable LC4_EnemyCreature action availability LA_POO with LO_PLAYER_DEFENCES
    disable LC4_EnemyCreature action availability LA_POO with LO_PLAYER_STORAGE_PIT
    disable LC4_EnemyCreature action availability LA_POO with LO_PLAYER_BUILDING
    disable LC4_EnemyCreature action availability LA_PLAY
    disable LC4_EnemyCreature action availability LA_WATER with LO_PLAYER_VILLAGER
    disable LC4_EnemyCreature action availability LA_WATER with LO_PLAYER_PLATOON
    disable LC4_EnemyCreature action availability LA_HEAL with LO_OPPONENT_PLATOON
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_CREATURE_PEN
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_ROCKS
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_MINE
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_ORE_ROCKS
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_TREES
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_FIELDS
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_POO
    disable LC4_EnemyCreature action availability LA_ATTACK with LO_SPELL_ORBS

    teach LC4_EnemyCreature all
    
    set player 1 creature to LC4_EnemyCreature
    
    wait 50 seconds //wait until intro finished
    
    release LC4_EnemyCreature

    run background script LC4_CreatureLogic

end script LC4_AICreature

begin script LC4_CreatureLogic

    Number = 0
    AITown[17]
    AICounter = -1
    CreatureRollRoleTimer = create timer for 0 seconds
    CreatureAttackTimer = create timer for 99999 seconds
    Position = 0
    oCreature = 0

start

begin loop
    if AICounter > 16
        AICounter = -1
    else
        AITown[AICounter] = get town with id AICounter
        if get building ABODE_NUMBER_TOWN_CENTRE in AITown[AICounter] min built 1.0 not exists
            AITown[AICounter] = 0
        end if
    end if

    if HEALTH of LC4_EnemyCreature == 0.00
        wait until HEALTH of LC4_EnemyCreature > 0.00
        set creature LC4_EnemyCreature role to CR_NONE radius 500 at {NorseCapital}
        HEALTH of LC4_EnemyCreature = 1.0
    end if
    
    if town AITown[AICounter] is under takeover from player 0 and PLAYER of AITown[AICounter] == 1 and HEALTH of LC4_EnemyCreature > 0
        HEALTH of LC4_EnemyCreature = 1.0
        set creature LC4_EnemyCreature role to CR_SOLDIER radius 100 at {AITown[AICounter]}
        
        wait until HEALTH of LC4_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds

    elsif PLAYER of AITown[AICounter] == 1 and get distance from {AITown[AICounter]} to {MyCreature} < 300 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00 and HEALTH of LC4_EnemyCreature > 0
        HEALTH of LC4_EnemyCreature = 1.0
        while HEALTH of LC4_EnemyCreature > 0.00 and get distance from {AITown[AICounter]} to {MyCreature} <= 400 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00
            wait 15 seconds
            set creature LC4_EnemyCreature role to CR_SOLDIER radius 33 at {MyCreature}
        end while

    elsif ENEMY_CREATURE_ALLOWED_TO_ATTACK == 1 and HEALTH of LC4_EnemyCreature > 0.00 and Difficulty >= 3
        HEALTH of LC4_EnemyCreature = 1.0
        begin loop
            if LC4_Platoon[13] exists
                set creature LC4_EnemyCreature role to CR_SOLDIER radius 50 at {LC4_Platoon[13]}
            else
                set creature LC4_EnemyCreature role to CR_SOLDIER radius 150 at {CreatureAttackPos[0]}
            end if
            until HEALTH of LC4_EnemyCreature == 0.00 or 1000 seconds
        end loop
        ENEMY_CREATURE_ALLOWED_TO_ATTACK = 0

    elsif ENEMY_CREATURE_ALLOWED_TO_ATTACK == 0 and HEALTH of LC4_EnemyCreature > 0.00 and get CreatureRollRoleTimer time remaining <= 0
        if PLAYER of AITown[AICounter] == 1
            Position = marker at {AITown[AICounter]} + {number from 50 to -50,number from 50 to -50}
            if number from 1 to 3 == 1
                set creature LC4_EnemyCreature role to CR_GATHERER radius 200 at {Position}
            else
                set creature LC4_EnemyCreature role to CR_NONE radius 200 at {Position}
            end if
            CreatureRollRoleTimer = create timer for 360 seconds
        
        end if
    end if

    AICounter += number from 0 to 16
    until LandWon == 1
end loop

//wait until HEALTH of LC4_EnemyCreature == 0.00
wait until get distance from camera position to {LC4_EnemyCreature} < 500 and not {LC4_EnemyCreature} viewed
delete LC4_EnemyCreature

end script LC4_CreatureLogic