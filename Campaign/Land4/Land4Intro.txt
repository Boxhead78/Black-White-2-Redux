///////////////////////////////////////////////////////////////////////////////
//    Land 4 Intro
//    ~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

define script LC4_Intro

begin script LC4_Intro

    WindMixer = 0
    AtmosMixer = create mixer
    IntroCloud = 0

start

    IntroCloud = create SCRIPT_OBJECT_TYPE_WEATHER_THING WEATHER_INFO_MEDIUM_CLOUD at {2166.29,600.00,1351.72}
    set cloud IntroCloud altitude 600
    set cloud IntroCloud points to 5

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds
    stop script "SavePersistentDataSetData"
    set persistent data "PREVLAND_OVER" to 0
    run script SavePersistentDataGetData

    WindMixer = create mixer
    set mixer WindMixer channel AUDIO_MIXER_CHANNEL_GLOBAL to 0.0

    set fade red 0 green 0 blue 0 time 0
    set game time 07.30

    wait 5 seconds
    set mixer WindMixer channel AUDIO_MIXER_CHANNEL_GLOBAL to 1.0 with fadetime 2
    wait 2 seconds
    destroy mixer WindMixer

    DM_DynamicMusicDisallowed = 1
    begin cinema
    
        disable atmos sound
        //set atmos volume 0
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.0
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.0
        start music "cut_scene_sad_34s_01" loop 3
        wait 2 seconds
        reset camera path
        queue camera move with position {2166.29,600.00,1351.72} focus {930.37,400.00,1083.87} time 0.0
        queue camera move with position {1353.72,450.00,1266.98} focus {875.93,300.00,1078.53} time 17.0
        queue camera move with position {784.42,310.00,1127.01} focus {720.90,276.95,1139.57} time 9.0
        
        disable load screen
        set fade in time 5    
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALTIME
        wait 8 seconds
        make good spirit appear
        make evil spirit appear
        wait 1 seconds
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_160"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_170"
        wait until read
        wait 2.5 seconds
        say "BW2T_SCRIPT_04FINAL_ADVISORS_LAND_DONE_210"
        wait until read
        wait until camera ready
        
        reset camera path 
        queue camera move with position {1295.12,200.00,1163.74} focus {934.80,195.00,1057.59} time 7.0
        queue camera move with position {1877.00,125.00,1287.28} focus {1501.49,100.00,1185.90} time 6.0
        queue camera move with position {2244.54,90.00,1150.90} focus {PlayerTown} time 4.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALTIME
        wait 0.5 seconds
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_26"
        wait until read
        wait 0.5 seconds
        if get player 0 alignment > 0.05
            say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_80"
            wait until read
        else
            say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_50"
            wait until read
        end if
        make evil spirit disappear
        make good spirit disappear

        wait 7 seconds    
    
        stop music with fadetime 4
        enable atmos sound
        //set atmos volume 0.45
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.45
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.45
    end cinema
    DM_DynamicMusicDisallowed = 0
    
    delete IntroCloud

    wait 2 seconds
    
    IntroFinished = 1

end script LC4_Intro