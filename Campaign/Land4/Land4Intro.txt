///////////////////////////////////////////////////////////////////////////////
//    Land 4 Intro
//    ~~~~~~~~~~~~~~
//
//    Start date:        31.10.16            By: Boxhead
//    Update date:    06.11.16            By: Boxhead
///////////////////////////////////////////////////////////////////////////////

define script LC4_Intro

begin script LC4_Intro

    WindMixer = 0
    AtmosMixer = create mixer

start

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 1 or 10 seconds
    stop script "SavePersistentDataSetData"
    set persistent data "PREVLAND_OVER" to 0
    run script SavePersistentDataGetData

    WindMixer = create mixer
    set mixer WindMixer channel AUDIO_MIXER_CHANNEL_GLOBAL to 0.0

    set fade red 0 green 0 blue 0 time 0
    set game time 06.00

    wait 5 seconds
    set mixer WindMixer channel AUDIO_MIXER_CHANNEL_GLOBAL to 1.0 with fadetime 2
    wait 2 seconds
    destroy mixer WindMixer

    begin cinema
    
        disable atmos sound
        set atmos volume 0
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.0
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.0
        start music "cut_scene_sad_34s_01" loop 3
        wait 2 seconds
        reset camera path
        queue camera move with position {1281.73,550.00,390.71} focus {1307.59,450.75,1937.77} time 0.0
        queue camera move with position {1284.19,380.85,1770.92} focus {1307.59,320.75,1937.77} + {5.00,0.00,-5.00} time 21.0
        queue camera move with position {1304.74,350.88,1893.16} focus {1307.59,320.75,1937.77} time 7.0
        
        disable load screen
        set fade in time 5    
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALTIME
        wait 4 seconds
        make good spirit appear
        make evil spirit appear
        wait 1 seconds
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_160"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_170"
        wait until read
        
        /*say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_180"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_190"
        wait until read*/
        make good spirit play across 0.5 down 0.5 HELPDUDE_KM_04FINAL_INTRO_G_180_190
        make evil spirit play across 0.5 down 0.5 HELPDUDE_KM_04FINAL_INTRO_E_180_190


        wait until read

        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_200"
        wait until read
        
        reset camera path 
           queue camera move with position {1226.21,340.00,1705.19} focus {1307.59,320.75,1937.77} time 6.0
           queue camera move with position {1228.91,270.00,1223.69} focus {1317.55,256.19,1599.79} time 6.0
           queue camera move with position {1208.90,200.00,913.21} focus {1318.19,205.80,1269.41} time 5.0
           queue camera move with position {1392.86,140.00,474.62} focus {1290.20,96.99,551.77} time 5.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALTIME
        wait 4 seconds
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_210"
        wait until read
        say "BW2T_SCRIPT_04FINAL_ADVISORS_INTRO_220"
        wait until read
        make evil spirit disappear
        make good spirit disappear

        wait 9 seconds    
    
        stop music with fadetime 4
        enable atmos sound
        set atmos volume 0.45
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_MAP to 0.6
        set mixer AtmosMixer channel AUDIO_MIXER_CHANNEL_ATMOS_POSITIONAL to 0.6
    end cinema
    
    wait 2 seconds
    
    IntroFinished = 1

end script LC4_Intro