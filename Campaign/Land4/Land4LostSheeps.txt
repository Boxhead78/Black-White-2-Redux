///////////////////////////////////////////////////////////////////////////////
//    Lost Sheeps
//    ~~~~~~~~~~~~~
//
//    Description:
//    The farmer slept and his sheeps ran aways.
//    It's up to the player to find and bring back the sheeps.
//  
//    Rewards:            20000 tribute
//
///////////////////////////////////////////////////////////////////////////////

define script LC4_LostSheeps
define script LC4_LostSheeps_Intro
define script LC4_LostSheeps_ExtraAnimations1
define script LC4_LostSheeps_Guy1Behaviour
define script LC4_LostSheeps_ConditionCheck
define script LC4_LostSheeps_OutroGood
define script LC4_LostSheeps_OutroEvil

global Scroll_LostSheeps = 0
global LostSheeps_ScrollPos = 0
global LostSheeps_Abode = 0
global LostSheeps_Influence1 = 0
global LostSheeps_Influence2 = 0
global LostSheeps_Influence3 = 0
global LostSheeps_Influence4 = 0
global LostSheeps_Influence5 = 0 
global LostSheeps_Guy1Pos = 0
global LostSheeps_Guy1 = 0
global LostSheeps_Sheep1 = 0
global LostSheeps_Sheep2 = 0
global LostSheeps_Sheep3 = 0
global LostSheeps_Sheep4 = 0
global LostSheeps_Sheep1Pos = 0
global LostSheeps_Sheep2Pos = 0
global LostSheeps_Sheep3Pos = 0
global LostSheeps_Sheep4Pos = 0
global LostSheeps_SheepDropPos = 0
global AllSheepsRescued = 0
global SheepsRescued = 0
global SheepsDead = 0

global L4ShowingLostSheepsObjective = 0

begin script LC4_LostSheeps

    ReminderTimer = create timer for 0 seconds
 
start

    begin loop
        wait 5 seconds
        LostSheeps_Abode = get HOUSE at {1672.02,230.81,1297.55} radius 15
        until LostSheeps_Abode exists
    end loop
    LostSheeps_ScrollPos = marker at {1677.02,230.81,1306.32} + {0.00,5.00,0.00}
    LostSheeps_Guy1Pos = marker at {1667.25,230.81,1299.46}
    LostSheeps_Sheep1Pos = marker at {1282.11,244.40,877.38}
    LostSheeps_Sheep2Pos = marker at {1432.07,230.72,1278.30}
    LostSheeps_Sheep3Pos = marker at {1942.54,169.13,1498.13}
    LostSheeps_Sheep4Pos = marker at {1352.46,217.46,640.48}
    LostSheeps_SheepDropPos = marker at {1663.16,230.76,1303.21}
    
    LostSheeps_Sheep1 = create ANIMAL SHEEP at {LostSheeps_Sheep1Pos}
    SCALE of LostSheeps_Sheep1 = 1.75
    LostSheeps_Sheep2 = create ANIMAL SHEEP at {LostSheeps_Sheep2Pos}
    SCALE of LostSheeps_Sheep2 = 1.75
    LostSheeps_Sheep3 = create ANIMAL SHEEP at {LostSheeps_Sheep3Pos}
    SCALE of LostSheeps_Sheep3 = 1.75
    LostSheeps_Sheep4 = create ANIMAL SHEEP at {LostSheeps_Sheep4Pos}
    SCALE of LostSheeps_Sheep4 = 1.75

    enable LostSheeps_Abode indestructible
    disable LostSheeps_Abode pickup
    disable LostSheeps_Abode moveable
    
    if Language == German
        Scroll_LostSheeps = create highlight SILVER name "HELP_TEXT_DIALOG_MULTIPLAYER_MAP_04" remind "HELP_TEXT_CREATURE_MENTAL_PROBLEM_01" at {LostSheeps_ScrollPos}
    elsif Language == English or Language == Spanish
        Scroll_LostSheeps = create highlight SILVER name "HELP_TEXT_MULTIPLAYER_SETUP_SPELL_11" remind "HELP_TEXT_CREATURE_MENTAL_PROBLEM_01" at {LostSheeps_ScrollPos}
    end if

    wait until Scroll_LostSheeps right clicked
    
    delete Scroll_LostSheeps
    
    LostSheeps_Influence1 = create influence at {LostSheeps_SheepDropPos} radius 15
    LostSheeps_Influence2 = create influence at {LostSheeps_Sheep1} radius 5
    LostSheeps_Influence3 = create influence at {LostSheeps_Sheep2} radius 5
    LostSheeps_Influence4 = create influence at {LostSheeps_Sheep3} radius 5
    LostSheeps_Influence5 = create influence at {LostSheeps_Sheep4} radius 5
    
    run background script LC4_LostSheeps_Intro

end script LC4_LostSheeps

begin script LC4_LostSheeps_Intro

start

    LostSheeps_Guy1 = create VILLAGER VILLAGER_INFO_NORSE_FORESTER_MALE at {LostSheeps_Abode}
    move LostSheeps_Guy1 position to {LostSheeps_Guy1Pos}
    
    disable LostSheeps_Guy1 pickup
    disable LostSheeps_Guy1 moveable

    wait 2 seconds
    
    DM_DynamicMusicDisallowed = 1
    begin cinema
        start music "lc4_lostsheeps_intro" loop 1
        move camera position to {1665.13,232.00,1305.33} time 7
        move camera focus to {LostSheeps_Guy1Pos} + {-1.00,1.50,1.00} time 7
        wait 1 seconds
        run background script LC4_LostSheeps_ExtraAnimations1
        wait 13 seconds
        set camera position to {1289.33,246.24,870.86}
        set camera focus to {1168.75,247.00,1130.81}
        move camera position to {1289.33,245.74,870.86} + {-3.00,3.50,3.00} time 9
        move camera focus to {1168.75,215.00,1130.81} time 9
        wait 7 seconds
        set camera position to {1665.13,232.00,1305.33}
        set camera focus to {LostSheeps_Guy1Pos} + {-1.00,1.50,1.00}
        wait 2 seconds
        move camera position to {1665.13,232.00,1305.33} + {-35.00,15.00,20.00} time 6
        move camera focus to {LostSheeps_Guy1Pos} + {-1.00,1.50,1.00} time 6
        wait 3 seconds
    end cinema
    DM_DynamicMusicDisallowed = 0
    
    run background script LC4_LostSheeps_ConditionCheck
    
    if Language == German
        Scroll_LostSheeps = create highlight SILVER name "HELP_TEXT_DIALOG_MULTIPLAYER_MAP_04" remind "HELP_TEXT_CREATURE_MENTAL_PROBLEM_01" at {LostSheeps_ScrollPos}
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_1 with amount 4 text "HELP_TEXT_DIALOG_MULTIPLAYER_MAP_04" amount 4 description "HELP_TEXT_CREATURE_MENTAL_PROBLEM_01" start value 0 reward 20000
    elsif Language == English or Language == Spanish
        Scroll_LostSheeps = create highlight SILVER name "HELP_TEXT_MULTIPLAYER_SETUP_SPELL_11" remind "HELP_TEXT_CREATURE_MENTAL_PROBLEM_01" at {LostSheeps_ScrollPos}
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_1 with amount 4 text "HELP_TEXT_MULTIPLAYER_SETUP_SPELL_11" amount 4 description "HELP_TEXT_CREATURE_MENTAL_PROBLEM_01" start value 0 reward 20000
    end if
    L4ShowingLostSheepsObjective = 1

end script LC4_LostSheeps_Intro

begin script LC4_LostSheeps_ExtraAnimations1

start

play anim "a_p_yawn" on LostSheeps_Guy1 loop 1

wait 4 seconds

play anim "a_p_yawn2" on LostSheeps_Guy1 loop 1

wait 7 seconds

play anim "a_p_yawn" on LostSheeps_Guy1 loop 2

end script LC4_LostSheeps_ExtraAnimations1

begin script LC4_LostSheeps_Guy1Behaviour

start

begin loop
    if not {LostSheeps_Guy1} near {LostSheeps_Guy1Pos} radius 5
        move LostSheeps_Guy1 position to {LostSheeps_Guy1Pos}
    else
        wait 1 seconds
    end if
end loop

end script LC4_LostSheeps_Guy1Behaviour

begin script LC4_LostSheeps_ConditionCheck

    aa = 1
    bb = 1
    cc = 1
    dd = 1
    a = 0
    b = 0
    c = 0
    d = 0
    g = 0
    Sheepsremaining = 4

start

begin loop

    if {LostSheeps_Sheep1} near {LostSheeps_SheepDropPos} radius 15 and AllSheepsRescued == 0 and a == 0 and not LostSheeps_Sheep1 is HELD
        a = 1
        Sheepsremaining = Sheepsremaining -1
        if HEALTH of LostSheeps_Sheep1 == 0.00
            SheepsDead = SheepsDead + 1
        else
            SheepsRescued = SheepsRescued + 1
        end if
    end if
    
    if {LostSheeps_Sheep2} near {LostSheeps_SheepDropPos} radius 15 and AllSheepsRescued == 0 and b == 0 and not LostSheeps_Sheep2 is HELD
        b = 1
        Sheepsremaining = Sheepsremaining -1
        if HEALTH of LostSheeps_Sheep2 == 0.00
            SheepsDead = SheepsDead + 1
        else
            SheepsRescued = SheepsRescued + 1
        end if
    end if
    
    if {LostSheeps_Sheep3} near {LostSheeps_SheepDropPos} radius 15 and AllSheepsRescued == 0 and c == 0 and not LostSheeps_Sheep3 is HELD
        c = 1
        Sheepsremaining = Sheepsremaining -1
        if HEALTH of LostSheeps_Sheep3 == 0.00
            SheepsDead = SheepsDead + 1
        else
            SheepsRescued = SheepsRescued + 1
        end if
    end if
    
    if {LostSheeps_Sheep4} near {LostSheeps_SheepDropPos} radius 15 and AllSheepsRescued == 0 and d == 0 and not LostSheeps_Sheep4 is HELD
        d = 1
        Sheepsremaining = Sheepsremaining -1
        if HEALTH of LostSheeps_Sheep4 == 0.00
            SheepsDead = SheepsDead + 1
        else
            SheepsRescued = SheepsRescued + 1
        end if
    end if
    
    if SheepsRescued == 1 and aa == 1
        aa = 2
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_1 value 1
        start music "lc4_sheep_1" loop 1
    end if
    
    if SheepsRescued == 2 and bb == 1
        bb = 2
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_1 value 2
        start music "lc4_sheep_2" loop 1
    end if
    
    if SheepsRescued == 3 and cc == 1
        cc = 2
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_1 value 3
        start music "lc4_sheep_3" loop 1
    end if
    
    if SheepsDead == 1 and dd == 1
        dd = 1
        start music "lc4_sheep_dead_1" loop 1
    end if
    
    if (Sheepsremaining == 0 or SheepsDead >= 2) and g == 0
        g = 1
    end if
    
    if HEALTH of LostSheeps_Guy1 == 0.00
        g = 1
    end if

    until g == 1
end loop

if HEALTH of LostSheeps_Guy1 > 0.00
    if SheepsDead < 2
        start music "lc4_sheep_end_1" loop 1
        wait 6 seconds
    end if

    if SheepsDead >= 2 or HEALTH of LostSheeps_Guy1 == 0.00
        run background script LC4_LostSheeps_OutroEvil
    else
        run background script LC4_LostSheeps_OutroGood
    end if
else
    delete LostSheeps_Influence1
    delete LostSheeps_Influence2
    delete LostSheeps_Influence3
    delete LostSheeps_Influence4
    delete LostSheeps_Influence5
    
    stop script "LC4_LostSheeps_Guy1Behaviour"
    
    release LostSheeps_Guy1
    release LostSheeps_Sheep1
    release LostSheeps_Sheep2
    release LostSheeps_Sheep3
    release LostSheeps_Sheep4

    wait 2 seconds
    delete Scroll_LostSheeps
end if

end script LC4_LostSheeps_ConditionCheck

begin script LC4_LostSheeps_OutroEvil

start

    start music "lc4_sheep_dead_2" loop 1

    delete LostSheeps_Influence1
    delete LostSheeps_Influence2
    delete LostSheeps_Influence3
    delete LostSheeps_Influence4
    delete LostSheeps_Influence5
    
    stop script "LC4_LostSheeps_Guy1Behaviour"
    
    release LostSheeps_Guy1
    release LostSheeps_Sheep1
    release LostSheeps_Sheep2
    release LostSheeps_Sheep3
    release LostSheeps_Sheep4

    PLAYER of LostSheeps_Guy1 = 0
    enable LostSheeps_Guy1 pickup
    enable LostSheeps_Guy1 moveable

    wait 2 seconds
    delete Scroll_LostSheeps
    
end script LC4_LostSheeps_OutroEvil

begin script LC4_LostSheeps_OutroGood

start

    start music "lc4_sheep_end_2" loop 1
    
    delete LostSheeps_Influence1
    delete LostSheeps_Influence2
    delete LostSheeps_Influence3
    delete LostSheeps_Influence4
    delete LostSheeps_Influence5
    
    stop script "LC4_LostSheeps_Guy1Behaviour"
    
    release LostSheeps_Guy1
    release LostSheeps_Sheep1
    release LostSheeps_Sheep2
    release LostSheeps_Sheep3
    release LostSheeps_Sheep4
    
    PLAYER of LostSheeps_Guy1 = 0
    enable LostSheeps_Guy1 pickup
    enable LostSheeps_Guy1 moveable

    wait 2 seconds
    delete Scroll_LostSheeps
    
    set player 0 objective TRIBUTE_OBJECTIVE_LAND_1 status to TRIBUTE_OBJECTIVE_STATE_COMPLETE

end script LC4_LostSheeps_OutroGood

//-----------------------------------------------------------------------------
// Land 4 Lost Sheeps
//-----------------------------------------------------------------------------