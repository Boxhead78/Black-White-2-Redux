///////////////////////////////////////////////////////////////////////////////
//    Land 9 Creature AI
//    ~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
//    Globals
//-----------------------------------------------------------------------------

define script LC9_CreatureAI
define script LC9_CreatureLogic

global CreatureAttackPos[2]
global CreatureGathererPos[4]

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

begin script LC9_CreatureAI

start

    CreatureAttackPos[0] = marker at {1394.29,43.69,325.80}//PlayerTown
    CreatureAttackPos[1] = marker at {1795.24,50.93,419.97}//PlayerTownFlank

    CreatureGathererPos[0] = marker at {287.94,228.81,2077.91}//AztecCapital
    CreatureGathererPos[1] = marker at {1030.79,22.48,1251.57}//AztecTown1
    CreatureGathererPos[2] = marker at {1904.18,25.86,1017.56}//AztecTown2
    CreatureGathererPos[3] = marker at {629.28,43.18,936.26}//AztecTown3

    LC9_EnemyCreature = create SCRIPT_OBJECT_TYPE_CREATURE CREATURE_TYPE_GORILLA at {382.16,262.31,2208.51}
    
    set creature LC9_EnemyCreature happiness to maximum
    
    set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_HEAL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_BUILDER_2 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_GATHERER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_SOLDIER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

    FireballAvailable[1] = 1
    LightningAvailable[1] = 1
    FireballMedRangeAvailable[1] = 1
    FireballHighRangeAvailable[1] = 1

    set creature LC9_EnemyCreature CREATURE_SIZE 8.0
    CREATURE_STRENGTH of LC9_EnemyCreature = 0.9
    CREATURE_ALIGNMENT of LC9_EnemyCreature = -0.75
    CREATURE_FATNESS of LC9_EnemyCreature = 0.5

    teach LC9_EnemyCreature all

    disable LC9_EnemyCreature action availability LA_EAT with LO_PLAYER_VILLAGER
    disable LC9_EnemyCreature action availability LA_EAT with LO_PLAYER_PLATOON
    disable LC9_EnemyCreature action availability LA_EAT with LO_ROCKS
    disable LC9_EnemyCreature action availability LA_EAT with LO_TREES
    disable LC9_EnemyCreature action availability LA_EAT with LO_TOYS
    disable LC9_EnemyCreature action availability LA_EAT with LO_POO
    disable LC9_EnemyCreature action availability LA_EAT with LO_SPELL_ORBS
    disable LC9_EnemyCreature action availability LA_POO with LO_PLAYER_VILLAGER
    disable LC9_EnemyCreature action availability LA_POO with LO_PLAYER_PLATOON
    disable LC9_EnemyCreature action availability LA_POO with LO_PLAYER_DEFENCES
    disable LC9_EnemyCreature action availability LA_POO with LO_PLAYER_STORAGE_PIT
    disable LC9_EnemyCreature action availability LA_POO with LO_PLAYER_BUILDING
    disable LC9_EnemyCreature action availability LA_PLAY
    disable LC9_EnemyCreature action availability LA_WATER with LO_PLAYER_VILLAGER
    disable LC9_EnemyCreature action availability LA_WATER with LO_PLAYER_PLATOON
    disable LC9_EnemyCreature action availability LA_HEAL with LO_OPPONENT_PLATOON
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_CREATURE_PEN
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_ROCKS
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_MINE
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_ORE_ROCKS
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_TREES
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_FIELDS
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_POO
    disable LC9_EnemyCreature action availability LA_ATTACK with LO_SPELL_ORBS
    
    set player 1 creature to LC9_EnemyCreature
    
    wait until IntroFinished == 1
    
    release LC9_EnemyCreature

    run background script LC9_CreatureLogic

end script LC9_CreatureAI

begin script LC9_CreatureLogic

    Number = 0
    AITown[17]
    AICounter = -1
    CreatureRollRoleTimer = create timer for 0 seconds
    Position = 0
    CreatureScoutTimer = create timer for 360 seconds
    CreatureScoutDone = 0

start

begin loop

    if AICounter > 16
        AICounter = -1
    else
        AITown[AICounter] = get town with id AICounter
        if get building ABODE_NUMBER_TOWN_CENTRE in AITown[AICounter] min built 1.0 not exists
            AITown[AICounter] = 0
        end if
    end if

    if HEALTH of LC9_EnemyCreature == 0.00
        wait until HEALTH of LC9_EnemyCreature > 0.00
        set creature LC9_EnemyCreature role to CR_NONE radius 500 at {AztecCapital}
        HEALTH of LC9_EnemyCreature = 1.0
    end if
    
    if town AITown[AICounter] is under takeover from player 0 and PLAYER of AITown[AICounter] == 1 and HEALTH of LC9_EnemyCreature > 0
        HEALTH of LC9_EnemyCreature = 1.0
        set creature LC9_EnemyCreature role to CR_SOLDIER radius 100 at {AITown[AICounter]}
        
        wait until HEALTH of LC9_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds

    elsif PLAYER of AITown[AICounter] == 1 and get distance from {AITown[AICounter]} to {MyCreature} < 300 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00 and HEALTH of LC9_EnemyCreature > 0
        HEALTH of LC9_EnemyCreature = 1.0
        while HEALTH of LC9_EnemyCreature > 0.00 and get distance from {AITown[AICounter]} to {MyCreature} <= 400 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00
            wait 15 seconds
            set creature LC9_EnemyCreature role to CR_SOLDIER radius 33 at {MyCreature}
        end while

    elsif LC9_Platoon[35] exists and LC9_Platoon[37] exists and LC9_Platoon[39] exists
        if HEALTH of LC9_EnemyCreature > 0.00
            Number = number from 1 to 2
            HEALTH of LC9_EnemyCreature = 1.0
            begin loop
                if LC9_Platoon[35] exists
                    set creature LC9_EnemyCreature role to CR_SOLDIER radius 75 at {LC9_Platoon[35]}
                elsif LC9_Platoon[37] exists
                    set creature LC9_EnemyCreature role to CR_SOLDIER radius 75 at {LC9_Platoon[37]}
                elsif LC9_Platoon[39] exists
                    set creature LC9_EnemyCreature role to CR_SOLDIER radius 75 at {LC9_Platoon[39]}
                end if
                until LC9_Platoon[35] not exists and LC9_Platoon[37] not exists and LC9_Platoon[39] not exists or 600 seconds or HEALTH of LC9_EnemyCreature == 0.00
            end loop
            if Number == 1
                set creature LC9_EnemyCreature role to CR_SOLDIER radius 125 at {CreatureAttackPos[0]}
            else
                set creature LC9_EnemyCreature role to CR_SOLDIER radius 125 at {CreatureAttackPos[1]}
            end if
            wait until HEALTH of LC9_EnemyCreature == 0.00 or 180 seconds
        end if

    elsif HEALTH of LC9_EnemyCreature > 0.00 and get CreatureRollRoleTimer time remaining <= 0
        if PLAYER of AITown[AICounter] == 1
            Position = marker at {AITown[AICounter]} + {number from 50 to -50,number from 50 to -50}
            if number from 1 to 3 == 1
                set creature LC9_EnemyCreature role to CR_GATHERER radius 200 at {Position}
            else
                set creature LC9_EnemyCreature role to CR_NONE radius 200 at {Position}
            end if
            CreatureRollRoleTimer = create timer for 360 seconds
        
        end if
        
    end if

    AICounter += number from 0 to 16
    until LandWon == 1
end loop

//wait until HEALTH of LC9_EnemyCreature == 0.00
wait until get distance from camera position to {LC9_EnemyCreature} < 500 and not {LC9_EnemyCreature} viewed
delete LC9_EnemyCreature

end script LC9_CreatureLogic