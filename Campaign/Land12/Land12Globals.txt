///////////////////////////////////////////////////////////////////////////////
//    Land 12 Globals
//    ~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

// Other Defines
define oCreature = 0
define script Globals
define script LC12_CreateUndeadPlatoon(whichPlatoon,howManyVillagers,whichMesh)
define script LC12_GhostScript(myGhost,whichMesh)
define script LC12_AIAltarCounter(whichBuilding)
define script LC12_DynamicDifficulty

define s_aztecwarrior_ghost = 0
define s_skeletonadultland2 = 1
define s_undeadwarrior_noghost = 2

// Debug switch
global DEBUG = 0
global Intro = 1

global LC12_EnemyCreature = 0
global LC12_EnemyCreatureAllowed = 0
global LC12_EnemyCreatureSpawned = 0
global CinemaIsRunning = 0
global MigrationsAccepted = 0
global TownsConverted = 0
global TownsAggressiveConverted = 0
global TownsPeacefulConverted = 0
global UnderAttack = 0
global IntroFinished = 0
global DeleteWeather = 0
global AttackingPlatoons = 0
global LC12_LargeSiegeLaunchAttack = 0
global LC12_LargeSiegeStartAttack = 0
global LC12_LargeSiegeRetreat = 0
global AztecPrisonDestroyed = 0
global AIHurricane = 0
global GenericCommentTimer = 0
global UnitDeadTimer = 0
global CreatureIntroDone = 0
global LC12_AltarCounter = 0
global HardMode = 0
global LandWon = 0
global PlayerAlignmentFinal = 0
global StaticPlatoonsActionTimer = 0
global StaticPlatoonsActive = 0
global RndAttack[5]
global ScriptedAttack[5]

global PlayerTown = 0
global MinorTown1 = 0
global MinorTown2 = 0
global MinorTown3 = 0
global MinorTown4 = 0
global AztecTown1 = 0
global AztecTown2 = 0
global AztecCapital = 0
global AztecPrison = 0
global NearestSettlement = 0

global MINOR_TOWN1_IMPRESSIVENESS = 6000
global MINOR_TOWN2_IMPRESSIVENESS = 12000
global MINOR_TOWN3_IMPRESSIVENESS = 18000
global MINOR_TOWN4_IMPRESSIVENESS = 24000
global AZTECTOWN1_IMPRESSIVENESS = 24500
global AZTECTOWN2_IMPRESSIVENESS = 20000
global AZTECCAPITAL_IMPRESSIVENESS = 31000

global AttackNearestTownTimer = 0
global LC12_RecruitTimer1 = 0
global LC12_RecruitTimer2 = 0
global LC12_RecruitTimer3 = 0

global L12ShowingNutOil2Objective = 0

begin script Globals

SecondPlaythrough = 0

start

    GenericCommentTimer = create timer for 0 seconds
    UnitDeadTimer = create timer for 200 seconds

    MINOR_TOWN1_IMPRESSIVENESS = MINOR_TOWN1_IMPRESSIVENESS * PLAYEREIMPRESSIVENESSMULTIPLIER
    MINOR_TOWN2_IMPRESSIVENESS = MINOR_TOWN2_IMPRESSIVENESS * PLAYEREIMPRESSIVENESSMULTIPLIER
    MINOR_TOWN3_IMPRESSIVENESS = MINOR_TOWN3_IMPRESSIVENESS * ImpressivenessModifier * PLAYEREIMPRESSIVENESSMULTIPLIER
    MINOR_TOWN4_IMPRESSIVENESS = MINOR_TOWN4_IMPRESSIVENESS * ImpressivenessModifier * PLAYEREIMPRESSIVENESSMULTIPLIER
    AZTECTOWN1_IMPRESSIVENESS = AZTECTOWN1_IMPRESSIVENESS * ImpressivenessModifier * PLAYEREIMPRESSIVENESSMULTIPLIER
    AZTECTOWN2_IMPRESSIVENESS = AZTECTOWN2_IMPRESSIVENESS * ImpressivenessModifier * PLAYEREIMPRESSIVENESSMULTIPLIER
    AZTECCAPITAL_IMPRESSIVENESS = AZTECCAPITAL_IMPRESSIVENESS * ImpressivenessModifier * PLAYEREIMPRESSIVENESSMULTIPLIER

    SecondPlaythrough = persistent data "SECOND_PLAYTHROUGH"
    HardMode = persistent data "HARD_MODE"

    if HardMode == 1
        AZTECTOWN1_IMPRESSIVENESS += 4000
        AZTECTOWN2_IMPRESSIVENESS += 4000
        AZTECCAPITAL_IMPRESSIVENESS += 4000
    end if

    if Difficulty >= 5
        AZTECTOWN1_IMPRESSIVENESS -= 2000
        AZTECTOWN2_IMPRESSIVENESS -= 2000
        AZTECCAPITAL_IMPRESSIVENESS += 4000
    end if

    ScriptedAttack[0] = 0
    ScriptedAttack[1] = 0
    ScriptedAttack[2] = 0
    ScriptedAttack[3] = 0
    ScriptedAttack[4] = 0

    if HardMode == 1
        NORSERECRUITMENTMODIFIER = 0.75
        JAPANESERECRUITMENTMODIFIER = 0.75
        AZTECRECRUITMENTMODIFIER = 0.75
        TALUSRECRUITMENTMODIFIER = 0.75
    end if

    GB_PlayerTown = get town with id 0//For Global Script
    PlayerTown = get town with id 0
    MinorTown1 = get town with id 3
    MinorTown2 = get town with id 8
    MinorTown3 = get town with id 11
    MinorTown4 = get town with id 12
    AztecTown1 = get town with id 4
    AztecTown2 = get town with id 5
    AztecCapital = get town with id 13
    AztecPrison = get town with id 6
    
    PlayerAlignmentFinal = get player 0 alignment
    StaticPlatoonsActionTimer = create timer for 1500 seconds

end script Globals


begin script LC12_CreateUndeadPlatoon(whichPlatoon,howManyVillagers,whichMesh)

    ArmyFlag = 0
    ArmySize = 0
    Ghost[225]
    counter = 0
    myEffect = 0
    Villager = 0
    VillagerTemp = 0

start

    attach 3d sound "SCRIPT24_SPOOKYLOOP" to whichPlatoon with volume 1.0

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {whichPlatoon} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {whichPlatoon} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 1.0

    myEffect = create visual effect VISUAL_MIRACLE_HEAL_GROUND at {whichPlatoon} + {0,2,0} time 12
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    //Remove old villagers
    ArmySize = get number of villagers in platoon whichPlatoon
    force while get number of villagers in platoon whichPlatoon > 1
        Villager = get villager from platoon whichPlatoon nearest {whichPlatoon}
        play anim "a_p_dead1" on Villager loop 1
        delete Villager
    end while
    Villager = get villager from platoon whichPlatoon nearest {whichPlatoon}
    SCALE of Villager = 0.01

    //set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_MASS of whichPlatoon to 1.0
    set platoon movement value FORMATION_MOVEMENT_STAT_ACCELERATION_MAX of whichPlatoon to 30.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MAX of whichPlatoon to 9.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MIN of whichPlatoon to 5.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_CHARGE_SPEED of whichPlatoon to 10.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_FIGHTER_SPEED of whichPlatoon to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_SPEED of whichPlatoon to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_CHARGE_SPEED of whichPlatoon to 10.0

    ArmyFlag = get SCRIPT_OBJECT_TYPE_ARMY_FLAG at {whichPlatoon} radius 50
    override mesh for ArmyFlag with "..\models\m_bannerundead"
    disable platoon whichPlatoon camp
    disable platoon whichPlatoon merge
    disable platoon whichPlatoon split
    disable platoon whichPlatoon can disband

    while counter < howManyVillagers and counter < ArmySize
        //Create ghosts
        Ghost[counter] = create VILLAGER AZTEC_FORESTER at {whichPlatoon} + {number from -10 to 10,number from -10 to 10}
        myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Ghost[counter]} + {0,1,0} time 1.5
        set myEffect color red 148 green 0 blue 211
        SCALE of myEffect = 0.1
        add villager Ghost[counter] to platoon whichPlatoon
        disable anim events on Ghost[counter]
        disable Ghost[counter] hurt by fire
        disable Ghost[counter] set on fire
        run background script LC12_GhostScript(Ghost[counter],whichMesh)
        counter++
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Villager} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    play anim "a_p_dead1" on Villager loop 1
    delete Villager

end script LC12_CreateUndeadPlatoon


begin script LC12_GhostScript(myGhost,whichMesh)

    myEffect = 0
    PlayerAlignment = 0

start

    while myGhost exists and HEALTH of myGhost > 0.00 and LandWon == 0 and PLAYER of myGhost == 1
        if SPEED of myGhost < 1.0
            SPEED of myGhost = 1.0
        end if
        override myGhost anim "a_p_run_male_miserable"
        if whichMesh == s_aztecwarrior_ghost
            //set anim speed of myGhost to 0.75
            override mesh for myGhost with "..\skins\s_aztecwarrior_ghost"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
            disable myGhost can be fisted
        elsif whichMesh == s_skeletonadultland2
            //set anim speed of myGhost to 0.5
            override mesh for myGhost with "..\skins\s_skeletonadultland2"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
        else
            //set anim speed of myGhost to 0.6
            override mesh for myGhost with "..\skins\s_undeadwarrior_noghost"
            set myGhost carrying CARRIED_OBJECT_NONE
            set myGhost carrying CARRIED_OBJECT_AZTEC_CLUB
        end if
        wait until HEALTH of myGhost == 0 or 60 seconds
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {myGhost} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    HEALTH of myGhost = 0.00

    begin loop
        if LandWon == 0
            if whichMesh == s_aztecwarrior_ghost
                //set anim speed of myGhost to 0.75
                override mesh for myGhost with "..\skins\s_aztecwarrior_ghost"
                set myGhost carrying CARRIED_OBJECT_NONE
                set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
                disable myGhost can be fisted
            elsif whichMesh == s_skeletonadultland2
                //set anim speed of myGhost to 0.5
                override mesh for myGhost with "..\skins\s_skeletonadultland2"
                set myGhost carrying CARRIED_OBJECT_NONE
                set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
            else
                //set anim speed of myGhost to 0.6
                override mesh for myGhost with "..\skins\s_undeadwarrior_noghost"
                set myGhost carrying CARRIED_OBJECT_NONE
                set myGhost carrying CARRIED_OBJECT_AZTEC_CLUB
            end if
        else
            play anim "a_p_dead1" on myGhost loop 1
            delete myGhost
        end if
        wait until HEALTH of myGhost == 0 or 60 seconds
        until not myGhost exists
    end loop

end script LC12_GhostScript


begin script LC12_AIAltarCounter(whichBuilding)

    AltarCount = 0

start

begin loop
    if HEALTH of whichBuilding == 1.0 and PLAYER of whichBuilding == 1 and AltarCount == 0
        AltarCount = 1
        LC12_AltarCounter += 1
    elsif AltarCount == 1
        if HEALTH of whichBuilding < 1.0 or PLAYER of whichBuilding != 1 or whichBuilding not exists
            AltarCount = 0
            LC12_AltarCounter -= 1
        end if
    end if
    until whichBuilding not exists and AltarCount == 0
end loop

end script LC12_AIAltarCounter


begin script LC12_DynamicDifficulty

    Counter = 0
    DifficultyTimer = create timer for 3600 seconds

    RecruitSizeModifierTemp = 0
    RecruitModifierTemp = 0
    EpicModifierTemp = 0
    BuilderModifierTemp = 0

start

//Game gets constantly easier
begin loop
    wait 2.5 seconds
    if get DifficultyTimer time remaining <= 0
        RecruitModifier *= 1.25
        EpicModifier *= 1.25
        BuilderModifier *= 1.25
        DifficultyTimer = create timer for 1800 seconds
        Counter++
    end if
    until Counter >= 3
end loop

//Store old values
RecruitSizeModifierTemp = RecruitSizeModifier
RecruitModifierTemp = RecruitModifier
EpicModifierTemp = EpicModifier
BuilderModifierTemp = BuilderModifier

//Give the AI a comeback for 30 minutes
RecruitSizeModifier = 1.25
//RecruitModifier = 1.25
BuilderModifier = 1.25
EpicModifier = 1.0

wait 1800 seconds

//Now restore old values
RecruitSizeModifier = RecruitSizeModifierTemp
RecruitModifier = RecruitModifierTemp
EpicModifier = EpicModifierTemp
BuilderModifier = BuilderModifierTemp

end script LC12_DynamicDifficulty