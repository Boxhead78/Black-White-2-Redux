///////////////////////////////////////////////////////////////////////////////
//    Land 12 Creature AI
//    ~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
//    Globals
//-----------------------------------------------------------------------------

define script LC12_AICreature
define script LC12_CreatureLogic

global CreatureAttackPos[3]
global CreatureBuildPos[2]
global CreatureGathererPos[3]

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

begin script LC12_AICreature

    CreaturePos = marker at {932.30,93.96,673.22}
    CSMixer = 0
    PlayerCreature = 0
    PlayerCreatureType = 0

start

    CreatureAttackPos[0] = marker at {1234.29,63.38,1467.94}//AztecCapital
    CreatureAttackPos[1] = marker at {988.19,42.13,1483.37}//AztecTown1
    CreatureAttackPos[2] = marker at {988.19,42.13,1483.37}//AztecTown2

    CreatureGathererPos[0] = marker at {1625.59,73.16,1511.93}//AztecCapital
    CreatureGathererPos[1] = marker at {514.55,14.95,1673.39}//AztecTown1
    CreatureGathererPos[2] = marker at {1026.14,26.25,923.02}//AztecTown2

    PlayerCreatureType = persistent data "PLAYER_CREATURE_TYPE"
    if PlayerCreatureType == variable CREATURE_TYPE_LION
        LC12_EnemyCreature = create CREATURE CREATURE_TYPE_LION at {0,0,0}

    elsif PlayerCreatureType == variable CREATURE_TYPE_APE 
        LC12_EnemyCreature = create CREATURE CREATURE_TYPE_APE at {0,0,0}

    elsif PlayerCreatureType == variable CREATURE_TYPE_GORILLA
        LC12_EnemyCreature = create CREATURE CREATURE_TYPE_GORILLA at {0,0,0}

    elsif PlayerCreatureType == variable CREATURE_TYPE_WOLF
        LC12_EnemyCreature = create CREATURE CREATURE_TYPE_WOLF at {0,0,0}

    elsif PlayerCreatureType == variable CREATURE_TYPE_COW
        LC12_EnemyCreature = create CREATURE CREATURE_TYPE_COW at {0,0,0}

    else// PlayerCreature == variable CREATURE_TYPE_TIGER
        LC12_EnemyCreature = create CREATURE CREATURE_TYPE_TIGER at {0,0,0}
        
    end if

    set creature LC12_EnemyCreature CREATURE_SIZE 0.25
    begin loop
        wait 2.5 seconds
        HEALTH of LC12_EnemyCreature = 1.0
        set LC12_EnemyCreature position to {0,0,0}
        until LC12_EnemyCreatureAllowed == 1 and HEALTH of LC12_EnemyCreature > 0.00
    end loop
    
    CinemaIsRunning = 1
    DM_DynamicMusicDisallowed = 1
    begin cinema
        store camera details
        set fade red 0 green 0 blue 0 time 4
        wait 4.1 seconds

        set LC12_EnemyCreature position to {CreaturePos}
        snap creature LC12_EnemyCreature focus to {AztecCapital}

        set creature LC12_EnemyCreature CREATURE_SIZE 1.0
        CREATURE_STRENGTH of LC12_EnemyCreature = 1.0
        CREATURE_ALIGNMENT of LC12_EnemyCreature = -0.85
        CREATURE_FATNESS of LC12_EnemyCreature = 0.5
        HEALTH of LC12_EnemyCreature = 1.0

        set camera position to {927.76,94.68,665.49}
        set camera focus to {AztecCapital} + {0,15,0}
        move camera position to {927.76,99.68,665.49} + {3,0,-9} time 24
        move camera focus to {AztecCapital} + {0,-125,0} time 24
        CSMixer = create mixer
        set mixer CSMixer channel AUDIO_MIXER_CHANNEL_MUSIC to 0.7
        start music "exintro1" loop 1
        set fade in time 4
        wait 1 seconds
        wait 2.25 seconds
        play anim C_FIGHT_THREAT2 on LC12_EnemyCreature
        wait 3.25 seconds
        play anim C_FIGHT_THREAT3 on LC12_EnemyCreature
        wait 3 seconds
        play anim C_FIGHT_THREAT1 on LC12_EnemyCreature
        set fade red 0 green 0 blue 0 time 4
        wait 4 seconds
        restore camera details
        set fade in time 4

    end cinema
    DM_DynamicMusicDisallowed = 0
    CinemaIsRunning = 0
    
    teach LC12_EnemyCreature all
    
    set player 1 creature to LC12_EnemyCreature
    
    set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_HEAL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_BUILDER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_GATHERER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_SOLDIER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

    FireballAvailable[1] = 1
    LightningAvailable[1] = 1
    FireballMedRangeAvailable[1] = 1
    FireballHighRangeAvailable[1] = 1

    disable LC12_EnemyCreature action availability LA_EAT with LO_PLAYER_VILLAGER
    disable LC12_EnemyCreature action availability LA_EAT with LO_PLAYER_PLATOON
    disable LC12_EnemyCreature action availability LA_EAT with LO_ROCKS
    disable LC12_EnemyCreature action availability LA_EAT with LO_TREES
    disable LC12_EnemyCreature action availability LA_EAT with LO_TOYS
    disable LC12_EnemyCreature action availability LA_EAT with LO_POO
    disable LC12_EnemyCreature action availability LA_EAT with LO_SPELL_ORBS
    disable LC12_EnemyCreature action availability LA_POO with LO_PLAYER_VILLAGER
    disable LC12_EnemyCreature action availability LA_POO with LO_PLAYER_PLATOON
    disable LC12_EnemyCreature action availability LA_POO with LO_PLAYER_DEFENCES
    disable LC12_EnemyCreature action availability LA_POO with LO_PLAYER_STORAGE_PIT
    disable LC12_EnemyCreature action availability LA_POO with LO_PLAYER_BUILDING
    disable LC12_EnemyCreature action availability LA_PLAY
    disable LC12_EnemyCreature action availability LA_WATER with LO_PLAYER_VILLAGER
    disable LC12_EnemyCreature action availability LA_WATER with LO_PLAYER_PLATOON
    disable LC12_EnemyCreature action availability LA_HEAL with LO_OPPONENT_PLATOON
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_CREATURE_PEN
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_ROCKS
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_MINE
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_ORE_ROCKS
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_TREES
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_FIELDS
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_POO
    disable LC12_EnemyCreature action availability LA_ATTACK with LO_SPELL_ORBS

    CreatureIntroDone = 1
    release LC12_EnemyCreature

    run background script LC12_CreatureLogic

    wait 45 seconds

    LC12_EnemyCreatureSpawned = 1
    set mixer CSMixer channel AUDIO_MIXER_CHANNEL_MUSIC to 1.0 with fadetime 10

end script LC12_AICreature

begin script LC12_CreatureLogic

    Number = 0
    AITown[17]
    AICounter = -1
    CreatureRollRoleTimer = create timer for 0 seconds
    Position = 0

start

begin loop

    if AICounter > 16
        AICounter = -1
    else
        AITown[AICounter] = get town with id AICounter
        if get building ABODE_NUMBER_TOWN_CENTRE in AITown[AICounter] min built 1.0 not exists
            AITown[AICounter] = 0
        end if
    end if

    if HEALTH of LC12_EnemyCreature == 0.00
        wait until HEALTH of LC12_EnemyCreature > 0.00
        set creature LC12_EnemyCreature role to CR_NONE radius 500 at {AztecCapital}
        HEALTH of LC12_EnemyCreature = 1.0
    end if
    
    if town AITown[AICounter] is under takeover from player 0 and PLAYER of AITown[AICounter] == 1 and HEALTH of LC12_EnemyCreature > 0
        HEALTH of LC12_EnemyCreature = 1.0
        set creature LC12_EnemyCreature role to CR_SOLDIER radius 100 at {AITown[AICounter]}
        
        wait until HEALTH of LC12_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds

    elsif PLAYER of AITown[AICounter] == 1 and get distance from {AITown[AICounter]} to {MyCreature} < 300 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00 and HEALTH of LC12_EnemyCreature > 0
        HEALTH of LC12_EnemyCreature = 1.0
        while HEALTH of LC12_EnemyCreature > 0.00 and get distance from {AITown[AICounter]} to {MyCreature} <= 400 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00
            wait 15 seconds
            set creature LC12_EnemyCreature role to CR_SOLDIER radius 33 at {MyCreature}
        end while

        wait until HEALTH of LC12_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds

    elsif LC12_Platoon[83] exists and LC12_Platoon[84] exists and LC12_Platoon[85] exists
        if HEALTH of LC12_EnemyCreature > 0.00
            HEALTH of LC12_EnemyCreature = 1.0
            begin loop
                if LC12_Platoon[83] exists
                    set creature LC12_EnemyCreature role to CR_SOLDIER radius 400 at {LC12_Platoon[83]}
                elsif LC12_Platoon[84] exists
                    set creature LC12_EnemyCreature role to CR_SOLDIER radius 400 at {LC12_Platoon[84]}
                elsif LC12_Platoon[85] exists
                    set creature LC12_EnemyCreature role to CR_SOLDIER radius 400 at {LC12_Platoon[85]}
                end if
                until (LC12_Platoon[83] not exists and LC12_Platoon[84] not exists and LC12_Platoon[85] not exists) or 720 seconds or HEALTH of LC12_EnemyCreature == 0.00
            end loop
            set creature LC12_EnemyCreature role to CR_SOLDIER radius 300 at {CreatureAttackPos[0]}
            wait until HEALTH of LC12_EnemyCreature == 0.00 or 360 seconds
        end if

    elsif LC12_Platoon[86] exists and LC12_Platoon[87] exists and LC12_Platoon[88] exists and LC12_Platoon[89] exists
        if HEALTH of LC12_EnemyCreature > 0.00
            HEALTH of LC12_EnemyCreature = 1.0
            begin loop
                if LC12_Platoon[86] exists
                    set creature LC12_EnemyCreature role to CR_SOLDIER radius 400 at {LC12_Platoon[86]}
                elsif LC12_Platoon[87] exists
                    set creature LC12_EnemyCreature role to CR_SOLDIER radius 400 at {LC12_Platoon[87]}
                elsif LC12_Platoon[88] exists
                    set creature LC12_EnemyCreature role to CR_SOLDIER radius 400 at {LC12_Platoon[88]}
                elsif LC12_Platoon[89] exists
                    set creature LC12_EnemyCreature role to CR_SOLDIER radius 400 at {LC12_Platoon[89]}
                end if
                until (LC12_Platoon[86] not exists and LC12_Platoon[87] not exists and LC12_Platoon[88] not exists and LC12_Platoon[89] not exists) or 720 seconds or HEALTH of LC12_EnemyCreature == 0.00
            end loop
            set creature LC12_EnemyCreature role to CR_SOLDIER radius 300 at {CreatureAttackPos[0]}
            wait until HEALTH of LC12_EnemyCreature == 0.00 or 360 seconds
        end if

    end if

    AICounter += number from 0 to 16
    until LandWon == 1
end loop

//wait until HEALTH of LC12_EnemyCreature == 0.00
wait until get distance from camera position to {LC12_EnemyCreature} < 500 and not {LC12_EnemyCreature} viewed
delete LC12_EnemyCreature

end script LC12_CreatureLogic