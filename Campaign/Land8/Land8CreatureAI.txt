///////////////////////////////////////////////////////////////////////////////
//    Land 8 Creature AI
//    ~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
//    Globals
//-----------------------------------------------------------------------------

define script LC8_CreatureAI
define script LC8_CreatureLogic

global CreatureAttackPos[3]
global CreatureGathererPos[3]

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

begin script LC8_CreatureAI

start

    CreatureAttackPos[0] = marker at {1278.60,151.48,867.86}//PlayerTown
    CreatureAttackPos[1] = marker at {1274.57,99.30,1219.41}//PlayerTownPreAttackPos
    CreatureAttackPos[2] = marker at {1666.55,157.00,810.82}//PlayerTownFlank

    CreatureGathererPos[0] = marker at {1752.24,148.78,1918.05}//JapaneseCapital
    CreatureGathererPos[1] = marker at {838.18,135.96,2030.69}//JapaneseTown1
    CreatureGathererPos[2] = marker at {1405.30,79.99,1534.11}//JapaneseTown2

    LC8_EnemyCreature = create SCRIPT_OBJECT_TYPE_CREATURE CREATURE_TYPE_TIGER at {1570.08,128.99,1976.78}
    
    set creature LC8_EnemyCreature happiness to maximum
    
    set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_HEAL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_BUILDER_2 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_GATHERER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
    set research ARTEFACT_CREATURE_ROLE_SOLDIER_2 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

    FireballAvailable[1] = 1
    LightningAvailable[1] = 1
    FireballMedRangeAvailable[1] = 1
    FireballHighRangeAvailable[1] = 0

    set creature LC8_EnemyCreature CREATURE_SIZE 0.8
    CREATURE_STRENGTH of LC8_EnemyCreature = 0.75
    ALIGNMENT of LC8_EnemyCreature = -0.4
    CREATURE_FATNESS of LC8_EnemyCreature = 0.4

    if HardMode == 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_WATER available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_LIGHTNING available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_FIREBALL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_MAGIC_TYPE_HEAL available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_ROLE_BUILDER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_ROLE_GATHERER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1
        set research ARTEFACT_CREATURE_ROLE_SOLDIER_3 available to RESEARCH_AVAILABILITY_RESEARCHED player 1

        FireballAvailable[1] = 1
        LightningAvailable[1] = 1
        FireballMedRangeAvailable[1] = 1
        FireballHighRangeAvailable[1] = 1

        set creature LC8_EnemyCreature CREATURE_SIZE 1.0
        CREATURE_STRENGTH of LC8_EnemyCreature = 1.0
        CREATURE_FATNESS of LC8_EnemyCreature = 0.5
    end if


    teach LC8_EnemyCreature all

    disable LC8_EnemyCreature action availability LA_EAT with LO_PLAYER_VILLAGER
    disable LC8_EnemyCreature action availability LA_EAT with LO_PLAYER_PLATOON
    disable LC8_EnemyCreature action availability LA_EAT with LO_ROCKS
    disable LC8_EnemyCreature action availability LA_EAT with LO_TREES
    disable LC8_EnemyCreature action availability LA_EAT with LO_TOYS
    disable LC8_EnemyCreature action availability LA_EAT with LO_POO
    disable LC8_EnemyCreature action availability LA_EAT with LO_SPELL_ORBS
    disable LC8_EnemyCreature action availability LA_POO with LO_PLAYER_VILLAGER
    disable LC8_EnemyCreature action availability LA_POO with LO_PLAYER_PLATOON
    disable LC8_EnemyCreature action availability LA_POO with LO_PLAYER_DEFENCES
    disable LC8_EnemyCreature action availability LA_POO with LO_PLAYER_STORAGE_PIT
    disable LC8_EnemyCreature action availability LA_POO with LO_PLAYER_BUILDING
    disable LC8_EnemyCreature action availability LA_PLAY
    disable LC8_EnemyCreature action availability LA_WATER with LO_PLAYER_VILLAGER
    disable LC8_EnemyCreature action availability LA_WATER with LO_PLAYER_PLATOON
    disable LC8_EnemyCreature action availability LA_HEAL with LO_OPPONENT_PLATOON
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_CREATURE_PEN
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_ROCKS
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_MINE
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_ORE_ROCKS
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_TREES
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_FIELDS
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_POO
    disable LC8_EnemyCreature action availability LA_ATTACK with LO_SPELL_ORBS
    
    set player 1 creature to LC8_EnemyCreature
    
    wait until IntroFinished == 1
    
    release LC8_EnemyCreature

    run background script LC8_CreatureLogic

end script LC8_CreatureAI

begin script LC8_CreatureLogic


    Number = 0
    AITown[17]
    AICounter = -1
    CreatureRollRoleTimer = create timer for 0 seconds
    Position = 0
    Altar1Pos = marker at {1299.71,132.52,986.85}//PlayerFrontWall
    Altar2Pos = marker at {1695.63,150.56,832.54}//PlayerFlankWall
    Altar1 = 0
    Altar2 = 0
    Altar1Built = 0
    Altar2Built = 0
    CreatureScoutTimer = create timer for 150 seconds
    CreatureScoutDone = 0

start

begin loop

    if AICounter > 16
        AICounter = -1
    else
        AITown[AICounter] = get town with id AICounter
        if get building ABODE_NUMBER_TOWN_CENTRE in AITown[AICounter] min built 1.0 not exists
            AITown[AICounter] = 0
        end if
    end if

    if HEALTH of LC8_EnemyCreature == 0.00
        wait until HEALTH of LC8_EnemyCreature > 0.00 or LandWon == 1
        set creature LC8_EnemyCreature role to CR_NONE radius 500 at {JapaneseCapital}
        HEALTH of LC8_EnemyCreature = 1.0
    end if
    
    if town AITown[AICounter] is under takeover from player 0 and PLAYER of AITown[AICounter] == 1 and HEALTH of LC8_EnemyCreature > 0
        HEALTH of LC8_EnemyCreature = 1.0
        set creature LC8_EnemyCreature role to CR_SOLDIER radius 100 at {AITown[AICounter]}
        
        wait until HEALTH of LC8_EnemyCreature == 0.00 or not town AITown[AICounter] is under takeover from player 0 or 1000 seconds or LandWon == 1

    elsif PLAYER of AITown[AICounter] == 1 and get distance from {AITown[AICounter]} to {MyCreature} < 300 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00 and HEALTH of LC8_EnemyCreature > 0
        HEALTH of LC8_EnemyCreature = 1.0
        while HEALTH of LC8_EnemyCreature > 0.00 and get distance from {AITown[AICounter]} to {MyCreature} <= 400 and get player 0 influence at {MyCreature} <= 0 and HEALTH of MyCreature > 0.00
            wait 15 seconds
            set creature LC8_EnemyCreature role to CR_SOLDIER radius 33 at {MyCreature}
        end while

    elsif AltarsActivated == 1 and HEALTH of LC8_EnemyCreature > 0.00 and Altar1Built == 0
        Altar1Built = 1
        HEALTH of LC8_EnemyCreature = 1.0
        set creature LC8_EnemyCreature role to CR_BUILDER radius 40 at {Altar1Pos}
        Altar1 = get HOUSE ALTAR at {Altar1Pos} radius 15
        wait until HEALTH of LC8_EnemyCreature == 0.00 or HEALTH of Altar1 == 1.0 or 300 seconds or Altar1 not exists or LandWon == 1
        set creature LC8_EnemyCreature role to CR_SOLDIER radius 100 at {Altar1Pos}
        wait until HEALTH of LC8_EnemyCreature == 0.00 or 180 seconds or HEALTH of Altar1 < 1.0 or Altar1 not exists or LandWon == 1

    elsif AltarsActivated == 1 and HEALTH of LC8_EnemyCreature > 0.00 and Altar2Built == 0
        Altar2Built = 1
        HEALTH of LC8_EnemyCreature = 1.0
        set creature LC8_EnemyCreature role to CR_BUILDER radius 40 at {Altar2Pos}
        Altar2 = get HOUSE ALTAR at {Altar2Pos} radius 15
        wait until HEALTH of LC8_EnemyCreature == 0.00 or HEALTH of Altar2 == 1.0 or 300 seconds or Altar2 not exists or LandWon == 1
        set creature LC8_EnemyCreature role to CR_SOLDIER radius 100 at {Altar2Pos}
        wait until HEALTH of LC8_EnemyCreature == 0.00 or 180 seconds or HEALTH of Altar2 < 1.0 or Altar2 not exists or LandWon == 1

    //Large Siege Attacks
    elsif LC8_LargeSiegeIsRunning == 1 and HEALTH of LC8_EnemyCreature > 0.00
        CreatureAttackTimer = create timer for 600 seconds
        HEALTH of LC8_EnemyCreature = 1.0
        if LC8_LargeSiegeIsRunning == 1
            begin loop
                set creature LC8_EnemyCreature role to CR_SOLDIER radius 100 at {CreatureAttackPos[1]}
                until HEALTH of LC8_EnemyCreature == 0.00 or LC8_LargeSiegeLaunchAttack == 1 or LandWon == 1
            end loop
        end if
        if LC8_LargeSiegeIsRunning == 1
            begin loop
                set creature LC8_EnemyCreature role to CR_SOLDIER radius 100 at {CreatureAttackPos[0]}
                until HEALTH of LC8_EnemyCreature == 0.00 or 500 seconds or LandWon == 1
            end loop
        end if

    //Scout PlayerTown Early
    elsif CreatureScoutDone == 0 and get CreatureScoutTimer time remaining <= 0 and HEALTH of LC8_EnemyCreature > 0.00
        CreatureScoutDone = 1
        HEALTH of LC8_EnemyCreature = 1.0
        set creature LC8_EnemyCreature role to CR_NONE radius 100 at {1258.21,123.73,1062.09}
        wait until HEALTH of LC8_EnemyCreature == 0.00 or 120 seconds or LandWon == 1
        set creature LC8_EnemyCreature role to CR_NONE radius 100 at {1051.87,103.25,755.82}
        wait until HEALTH of LC8_EnemyCreature == 0.00 or 45 seconds or LandWon == 1

    //Attack PlayerTown Early
    elsif LC8_Platoon[81] exists and LC8_Platoon[82] exists and LC8_Platoon[83] exists and Difficulty >= 4
        if HEALTH of LC8_EnemyCreature > 0.00
            HEALTH of LC8_EnemyCreature = 1.0
            begin loop
                if LC8_Platoon[81] exists
                    set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {LC8_Platoon[81]}
                elsif LC8_Platoon[82] exists
                    set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {LC8_Platoon[82]}
                elsif LC8_Platoon[83] exists
                    set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {LC8_Platoon[83]}
                end if
                until (LC8_Platoon[81] not exists and LC8_Platoon[82] not exists and LC8_Platoon[83] not exists) or 720 seconds or HEALTH of LC8_EnemyCreature == 0.00 or LandWon == 1
            end loop
            set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {CreatureAttackPos[2]}
            wait until HEALTH of LC8_EnemyCreature == 0.00 or 360 seconds or LandWon == 1
        end if

    //Attack PlayerTown Late
    elsif LC8_Platoon[72] exists or LC8_Platoon[73] exists or LC8_Platoon[74] exists
        if HEALTH of LC8_EnemyCreature > 0.00
            HEALTH of LC8_EnemyCreature = 1.0
            begin loop
                if LC8_Platoon[72] exists
                    set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {LC8_Platoon[72]}
                elsif LC8_Platoon[73] exists
                    set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {LC8_Platoon[73]}
                elsif LC8_Platoon[74] exists
                    set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {LC8_Platoon[74]}
                end if
                until (LC8_Platoon[72] not exists and LC8_Platoon[73] not exists and LC8_Platoon[74] not exists) or 720 seconds or HEALTH of LC8_EnemyCreature == 0.00 or LandWon == 1
            end loop
            set creature LC8_EnemyCreature role to CR_SOLDIER radius 75 at {CreatureAttackPos[0]}
            wait until HEALTH of LC8_EnemyCreature == 0.00 or 360 seconds or LandWon == 1
        end if

    elsif HEALTH of LC8_EnemyCreature > 0.00 and get CreatureRollRoleTimer time remaining <= 0
        if PLAYER of AITown[AICounter] == 1
            Position = marker at {AITown[AICounter]} + {number from 50 to -50,number from 50 to -50}
            if number from 1 to 3 == 1
                set creature LC8_EnemyCreature role to CR_GATHERER radius 200 at {Position}
            else
                set creature LC8_EnemyCreature role to CR_NONE radius 200 at {Position}
            end if
            CreatureRollRoleTimer = create timer for 360 seconds
        
        end if
        
    end if

    AICounter += number from 0 to 16
    until LandWon == 1
end loop

//wait until HEALTH of LC8_EnemyCreature == 0.00
wait until get distance from camera position to {LC8_EnemyCreature} < 500 and not {LC8_EnemyCreature} viewed
delete LC8_EnemyCreature

end script LC8_CreatureLogic