///////////////////////////////////////////////////////////////////////////////
//    Land 8 Outro
//    ~~~~~~~~~~~~~
//
//    Start date:     28.03.18            By: Boxhead
//    Update date:    04.04.18            By: Boxhead
///////////////////////////////////////////////////////////////////////////////

define script LC8_Outro
define script LC8_Departure

begin script LC8_Outro

CSMixer = 0
JapaneseL = 0
JapaneseR = 0
Town6 = get town with id 6
Town5 = get town with id 5
PlayerAlignment = 0

start

    wait until get player 1 town total == 0
    LandWon = 1

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds
    stop script "SavePersistentDataSetData"
    set persistent data "PREVLAND_OVER" to 1
    run script SavePersistentDataGetData

    set town PlayerTown capture all enemy towns
    disable dialogue manager
    disable game can be lost
    //increment tribute by 250000
    if PLAYER of JapaneseCapital == 0
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 250000 alignment -0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature - 0.10
    else
        set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND with amount 1 text "BW2T_SCRIPT_07FINAL_SECONDARYOBJECTIVE_80" amount 1 description "BW2T_SCRIPT_06FINAL_SECONDARYOBJECTIVE_51" start value 0 reward 250000 alignment 0.05
        CREATURE_ALIGNMENT of MyCreature = CREATURE_ALIGNMENT of MyCreature + 0.10
    end if
    set player 0 objective TRIBUTE_OBJECTIVE_SCRIPT_CONTROLLED_WIN_LAND value 1
    stop script "LC8_Speeches"
    stop script "LC8_GateHouseManager"
    stop script "LC8_CreatureLogic"
    stop script "LC8_MusicManager"

    wait 2 seconds

    CSMixer = create mixer
    set mixer CSMixer channel AUDIO_MIXER_CHANNEL_MUSIC to 1.0 with fadetime 4
    stop dynamic music

    JapaneseL = create VILLAGER VILLAGER_INFO_TARANAGA at {1454.28,247.09,1735.31}
    JapaneseR = create VILLAGER VILLAGER_INFO_HIROKU at {1449.55,246.58,1737.32}
    
    set JapaneseL focus to {1151.01,122.88,1033.51}
    set JapaneseR focus to {1151.01,122.88,1033.51}
    play anim "a_p_chatting_paired01_a" on JapaneseL loop -1
    play anim "a_p_talk2_male" on JapaneseR loop -1

    DM_DynamicMusicDisallowed = 1
    begin cinema
        //AGC: Moved fade out & wait inside the cinema block
        set fade red 0 green 0 blue 0 time 2
        wait 2 seconds

        start music "cut_scene_sad_34s_01" loop 100
        wait 2 seconds

    
        
        reset camera path
        queue camera move with position {1447.94,247.87,1720.74} focus {1455.66,248.18,1734.13} time 0.0
        queue camera move with position {1306.64,255.00,1573.06} focus {1455.66,245.18,1734.13} time 24.0
        
        queue camera move with position {1171.88,235.00,1381.06} focus {1455.66,227.18,1734.13} time 9.0
        
        queue camera move with position {1221.64,270.50,1001.68} focus {1393.72,195.00,1513.76} time 9.0
        
        queue camera move with position {1404.06,215.00,568.58} focus {PlayerTown} time 12.0
        play camera path with easein 20 easeout 8 method SCRIPT_CAMERAPATH_EQUALDISTANCE    
        set fade in time 2
        
        
        
        wait 2 seconds
        //Gen of city:The Greeks beat the Norse and then they humbled us. Come the final battle we must support them.        
        say "BW2T_SCRIPT_08FINAL_PDM_ENDLAND_10"
        wait until read
        //Gen of War:Agreed. Even the Teachings of War were no help against these Greeks. They are surely worthy of our respect.        
        say "BW2T_SCRIPT_08FINAL_PDM_ENDLAND_20"
        wait until read
        //Gen of War:But to defeat the Aztecs they will need guile and cunning.        
        say "BW2T_SCRIPT_08FINAL_PDM_ENDLAND_30"
        wait until read
        //Gen of War:They can stop the Aztec Epic Miracles if they are clever.        
        say "BW2T_SCRIPT_08FINAL_PDM_ENDLAND_40"
        wait until read
        //Gen of War:To do this the Greeks must cross the dead deserts to the Aztec sacrificial lands.        
        say "BW2T_SCRIPT_08FINAL_PDM_ENDLAND_50"
        wait until read
        
        if not PLAYER of Town5 == 0 or not PLAYER of Town6 == 0
            eject evil spirit
            say "BW2T_SCRIPT_08FINAL_ADVISORS_OUTRO_20"
            wait until read
            send evil spirit home
        end if
        
        wait until camera ready
        stop music with fadetime 5
        destroy mixer CSMixer

    end cinema
    DM_DynamicMusicDisallowed = 0

    run background script LC8_Departure

end script LC8_Outro

begin script LC8_Departure
    
start

    WT_SkyTransitionGlobalInfluence = 1
    enable global influence

    vtx_max_armies = 5
    vtx_max_sieges = 2
    vtx_max_orerocks = 7
    vtx_tree_max = 12
    //vtx_rock_max = 20
    vtx_ore_max = 20000
    vtx_food_max = 20000
    vtx_wood_max = 20000
    vtx_vill_max = 100
    vtx_max_arm_men = 75
    vtx_rock_max = 5
    vtx_size = 2.5
    vtx_radius = 30

    run script Vtx_Create_Exiting(1358.59,237.16,383.97)
    
    wait until vtx_scroll_clicked == VTX_TRUE

    release MyCreature
    MyCreature = get player 0 creature
    if MyCreature exists
        save my_creature
    end if
    
    // Get the dialogue for the enemy and for the advisors
    
    set fade red 0 green 0 blue 0 time 2
    wait 2 seconds
    DM_DynamicMusicDisallowed = 1
    begin cinema
        start music "farming_communtiy_land2" loop 10
        wait 2 seconds
    
        queue camera move with position {1358.59,237.16,383.97} + {10,7.5,10} focus {1358.59,237.16,383.97} + {0,5,0} time 0.0
        queue camera move with position {1358.59,237.16,383.97} + {7.5,5,7.5} focus {1358.59,237.16,383.97} time 10.0
        queue camera move with position {1358.59,237.16,383.97} + {1,1.5,1} focus {1358.59,237.16,383.97} time 6.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE
        set fade in time 1
        eject evil spirit
        eject good spirit
        say "BW2T_SCRIPT_08FINAL_ADVISORS_PLAYERWINS_30"
        wait until read
        say "BW2T_SCRIPT_08FINAL_ADVISORS_PLAYERWINS_40"
        wait until read
        send good spirit home
        send evil spirit home 
        set fade red 0 green 0 blue 0 time 3
        wait 3.5 seconds
        wait 1 seconds
    end cinema
    DM_DynamicMusicDisallowed = 0

    if L8ShowingChickenWolfGrainObjective == 0
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_7 with amount 1 text "BW2T_SCRIPT_08FINAL_CHALLENGE_CHICKEN_WOLF_GRAIN_MISC_40" amount 1 description "BW2T_SCRIPT_08FINAL_CHALLENGE_CHICKEN_WOLF_GRAIN_REMINDER" start value 0 reward 30000
    end if
    
    if L8ShowingOilBaronObjective == 0
        set player 0 objective TRIBUTE_OBJECTIVE_LAND_9 with amount 1 text "BW2T_SCRIPT_08FINAL_CHALLENGE_OIL_BARON_SCROLL_10" amount 1 description "BW2T_SCRIPT_08FINAL_CHALLENGE_OIL_BARON_REMINDER" start value 0 reward 30000
    end if
    
    if L8ShowingMonsterMineObjective  == 0
    set player 0 objective TRIBUTE_OBJECTIVE_LAND_10 with amount 1 text "BW2T_SCRIPT_08FINAL_MONSTERMINE_580" amount 1 description "BW2T_SCRIPT_08FINAL_MONSTERMINE_590" start value 0 reward GLOBAL_VALUE_TRIBUTE_LAND_8_MONSTERMINE    
    end if        
    
    ////
    //TRIGGER processing of land end objectives
    process land end objectives
    wait until land end objectives processed    
    
    disable game can be lost
    run script LandExit
    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds

    disable load screen
    load map "./Data/Landscape/BW2/Land9.bwe"

    wait 3 seconds

end script LC8_Departure