///////////////////////////////////////////////////////////////////////////////
//    Land 6 Intro
//    ~~~~~~~~~~~~~~
//
//    Start date:    05.02.17            By: Boxhead
//    Update date:    09.03.18            By: Boxhead
///////////////////////////////////////////////////////////////////////////////

define False = 0
define True = 1
define MAX_VILLAGERS = 100

//-----------------------------------------------------------

global ScriptFinished = False
global DeletePeople = False

define script LC6_Intro
define script LookAFterPeopleComingFromTeleporter(Portal)
//-----------------------------------------------------------

begin script LC6_Intro

    GreekArchers = 0
    Portal = 0
    LC6_GeneralsInitialArchersx = 0
    LC6_GeneralsInitialMeleex = 0
    WindMixer = 0

start

    PD_ForcedSaving = 1
    wait until PD_ForcedSaving == 0 or 10 seconds
    stop script "SavePersistentDataSetData"
    set persistent data "PREVLAND_OVER" to 0
    run script SavePersistentDataGetData

    WindMixer = create mixer
    set mixer WindMixer channel AUDIO_MIXER_CHANNEL_GLOBAL to 0.0

    Portal = create visual effect VISUAL_EFFECT_TELEPORTER_HOOP at {1630.60,73.16,1592.69} + {0, 3, 0} time -1
    
    GreekArchers = create platoon PLATOON_INFO_GREEK_RANGED_1 at {1097.15,23.16,989.94} with 50 men and 0 women
    PLAYER of GreekArchers = 0
    attach GreekArchers to PlayerTown
    enable platoon GreekArchers camp
    release GreekArchers

    LC6_GeneralsInitialArchersx = create platoon PLATOON_INFO_JAPANESE_RANGED_4 at {1639.27,72.78,1482.29} with 60 * RecruitSizeModifier men and 0 women
    attach LC6_GeneralsInitialArchersx to JapaneseCapital
    enable platoon LC6_GeneralsInitialArchersx camp
    set LC6_GeneralsInitialArchersx focus to {JapaneseCapital}

    LC6_GeneralsInitialMeleex = create platoon PLATOON_INFO_JAPANESE_MELEE_4 at {1669.44,72.88,1516.87} with 60 * RecruitSizeModifier men and 0 women
    attach LC6_GeneralsInitialMeleex to JapaneseCapital
    enable platoon LC6_GeneralsInitialMeleex camp
    set LC6_GeneralsInitialMeleex focus to {JapaneseCapital}

    wait 5 seconds
    set mixer WindMixer channel AUDIO_MIXER_CHANNEL_GLOBAL to 1.0 with fadetime 2
    wait 2 seconds
    destroy mixer WindMixer
    
    begin cinema
        start music "cut_scene_danger_01" loop 2
        set game time 04.40

        wait 2 seconds
    
        queue camera move with position {1313.69,240.00,688.46} focus {885.90,140.00,901.78} + {0.00,50.00,0.00} time 0.0
        queue camera move with position {1106.06,50.00,935.62} focus {1077.61,23.41,981.32} time 26.0
        queue camera move with position {648.23,190.00,1139.86} focus {610.49,205.77,1180.34} + {0.00,-50.00,0.00} time 9.0
        
        //queue camera move with position {868.973, 105.023, 673.833} focus {861.404, 57.735, 761.620} time 8.0
        queue camera move with position {701.93,120.00,1350.77} focus {883.98,50.00,1440.20} time 5.0
        queue camera move with position {927.74,60.00,1387.32} focus {977.21,40.00,1393.43} time 5.0
        
        //queue camera move with position {749.005, 138.561, 1129.055} focus {745.386, 112.169, 1225.442} time 5.0
        
        //queue camera move with position {710.346, 144.092, 1137.566} focus {729.786, 116.485, 1231.694} time 4.0
            queue camera move with position {1131.68,120.00,1353.61} focus {1636.13,73.16,1542.11} time 3.5
        
        //queue camera move with position {1058.682, 146.029, 1540.830} focus {1087.418, 110.839, 1629.917} time 4.0
        queue camera move with position {1634.79,85.00,1541.58} focus {1650.66,73.16,1568.16} time 8.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE
        
        disable load screen
        set fade red 0 green 0 blue 0 time 0
        set fade in time 4
        
        //run background script LandStart
                    
        set fade in time 4
        wait 3 seconds
        eject good spirit
        eject evil spirit

        // GA: This place doesn't look so bad. We should be able to take it over with little effort.
        make good spirit play across 0.5 down 0.5 HELPDUDE_KM_06FINAL_INTRO_G_10
        make evil spirit play across 0.5 down 0.5 HELPDUDE_KM_06FINAL_INTRO_E_10
        wait until read
        
        //say "BW2T_SCRIPT_06FINAL_INTRO_10"
        //wait until read
        
        //Great! More warfare! This is what makes my blood tingle!        
        
        //say "BW2T_SCRIPT_06FINAL_PDM_ENDLAND_INTRO_10"
        //wait until read
        
        //BUT if your impress
        //say "BW2T_SCRIPT_06FINAL_PDM_ENDLAND_INTRO_20"
        //wait until read
        
        say "BW2T_SCRIPT_06FINAL_INTRO_20"
        wait until read
        send good spirit home
        send evil spirit home
        
        wait 17 seconds
        // EA: Ha! There are some juicy Towns to ransack! I'm gonna like this place.
        say "BW2T_SCRIPT_06FINAL_INTRO_30"
        wait until read
        
        wait 2 seconds
        run background script LookAFterPeopleComingFromTeleporter(Portal)
        wait 4.5 seconds
        // EA: What? What's this? Another Portal? Who does that belong to?
        say "BW2T_SCRIPT_06FINAL_INTRO_40"
        wait until read
        wait until camera ready
        
        reset camera path 
        queue camera move with position {1664.54,90.23,1463.48} focus {1630.60,73.16,1592.69} time 14.0
        play camera path with easein 4 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE
        say "BW2T_SCRIPT_06FINAL_PDM_ENDLAND_INTRO_30"
        wait until read
        
        say "BW2T_SCRIPT_06FINAL_INTRO_60"
        wait until read
        
        
        reset camera path
        queue camera move with position {1337.44,90.11,1369.67} focus {JapaneseCapital} time 5.0
        queue camera move with position {1183.91,45.00,1111.65} focus {JapaneseCapital} time 5.0
        queue camera move with position {1065.73,50.50,922.56} focus {1089.10,23.41,976.78} time 5.0
        play camera path with easein 12 easeout 4 method SCRIPT_CAMERAPATH_EQUALDISTANCE
        eject good spirit
        eject evil spirit
        // GA: We'd better start building now. These Japanese seem to be formidable builders themselves.
        say "BW2T_SCRIPT_06FINAL_INTRO_70"
        wait until read

        // EA: And get some armies, Boss. They look pretty handy at fighting as well!
        say "BW2T_SCRIPT_06FINAL_INTRO_80"
        wait until read
        send good spirit home
        send evil spirit home
        DeletePeople = True
        wait 5 seconds
        wait until camera ready
        stop music with fadetime 5
    end cinema
    IntroFinished = 1
    delete LC6_GeneralsInitialArchersx
    delete LC6_GeneralsInitialMeleex
    
end script LC6_Intro

//-----------------------------------------------------------

begin script LookAFterPeopleComingFromTeleporter(Portal)
People[100]
Counter = 0
Effect = 0
start

    while Counter < 100
        People[Counter] = create random villager of tribe TRIBE_TYPE_JAPANESE at {1630.60,73.16,1592.69} + {number from -3 to 3, 0, number from -3 to 3}//create VILLAGER VILLAGER_INFO_JAPANESE_FORESTER_MALE at {1013.581, 132.333, 2002.798} + {number from -3 to 3, 0, number from -3 to 3}
        attach People[Counter] to JapaneseCapital
        move People[Counter] position to {1649.83,73.16,1541.60}        
        Effect = create visual effect "gp_s_teleporter_people.ves" strength 1 scale 1 at {People[Counter]}
        wait 0.3 seconds
        delete Effect
        Counter++
    end while
    
    wait until DeletePeople == True
    Counter = 0 
    force while Counter < 100
        release People[Counter]//delete People[Counter]
        Counter++
    end while
    
    delete Portal
    
end script LookAFterPeopleComingFromTeleporter

//-----------------------------------------------------------
