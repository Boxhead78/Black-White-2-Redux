///////////////////////////////////////////////////////////////////////////////
//           Undead Armies Script
//    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//
///////////////////////////////////////////////////////////////////////////////

define script UA_GraveyardCheck(PlayerID)
define script UA_SpawnUndeadPlatoonCheck(PlayerID,Graveyard,Flag,Foundation)
define script UA_CreateUndeadPlatoon(PlayerID,Graveyard,howManyVillagers)
define script UA_GhostScript(PlayerID,myGhost,myArmy)

global UA_myTempDeath[9]
global UA_myUsedUndead[9]

begin script UA_GraveyardCheck(PlayerID)

    FlagObject = 0
    myCounter = 0
    myGraveyard = 0
    myDeadPeople = 0
    myFlag = 0
    myFoundation = 0
    myTown[22]
    Counter = 0

start

begin loop
    if get player PlayerID alignment < -0.75
        if Counter > 20
            Counter = 0
        end if
        
        myDeadPeople = get total of stat STATS_EVT_SACRIFICES
        myTown[Counter] = get town with id Counter
        myGraveyard = get random abode of type ABODE_NUMBER_GRAVEYARD in town myTown[Counter]
        UA_myTempDeath[PlayerID] = (myDeadPeople * 1.5) - UA_myUsedUndead[PlayerID]
        FlagObject = get FEATURE FEATURE_INFO_SEVEN_SAMURAI_BASE at {myGraveyard} radius 10
        if HEALTH of myGraveyard >= 1.0 and FlagObject not exists and PLAYER of myGraveyard == PlayerID
            myFlag = create MOBILE_OBJECT ARMY_FLAG_INFO_GREEK_MELEE at {myGraveyard}
            override mesh for myFlag with "..\models\m_bannerundead"
            ALTITUDE of myFlag = 1.0
            ANGLE of myFlag = ANGLE of myGraveyard + 180
            SCALE of myFlag = 3.0
            disable myFlag pickup
            disable myFlag moveable
            enable myFlag indestructible
            myFoundation = create FEATURE FEATURE_INFO_SEVEN_SAMURAI_BASE at {myGraveyard}
            SCALE of myFoundation = 0.01
            disable myFoundation pickup
            disable myFoundation moveable
            enable myFoundation indestructible
            run background script UA_SpawnUndeadPlatoonCheck(PlayerID,myGraveyard,myFlag,myFoundation)
        end if
        Counter++
    else
        wait 5 seconds
    end if
end loop

end script UA_GraveyardCheck


begin script UA_SpawnUndeadPlatoonCheck(PlayerID,Graveyard,Flag,Foundation)

    myArmySize = 0
    Tooltip[2]

start

while HEALTH of Graveyard >= 1.0 and Graveyard exists and Flag exists and get player PlayerID alignment < -0.75
    if UA_myTempDeath[PlayerID] >= 30
        if hand position near {Flag} radius 4 and Tooltip[0] not exists
            Tooltip[0] = create tooltip on Flag with text "HELP_TEXT_PAUSE_STATS_95"
        end if
    else
        if hand position near {Flag} radius 4 and Tooltip[1] not exists
            Tooltip[1] = create tooltip on Flag with text "HELP_TEXT_PAUSE_STATS_111"
        end if
    end if
    if hand position not near {Flag} radius 4 + 1 and Tooltip[0] exists
        delete Tooltip[0]
    end if
    if hand position not near {Flag} radius 4 + 1 and Tooltip[1] exists
        delete Tooltip[1]
    end if

    if UA_myTempDeath[PlayerID] >= 30
        if UA_myTempDeath[PlayerID] >= 150
            myArmySize = 150
        elsif UA_myTempDeath[PlayerID] >= 120
            myArmySize = 120
        elsif UA_myTempDeath[PlayerID] >= 90
            myArmySize = 90
        elsif UA_myTempDeath[PlayerID] >= 60
            myArmySize = 60
        elsif UA_myTempDeath[PlayerID] >= 30
            myArmySize = 30
        end if
        if (bindable action BINDABLE_ACTION_TYPE_ACTION performed or bindable action BINDABLE_ACTION_TYPE_MOVE performed) and hand position near {Flag} radius 4
            run script UA_CreateUndeadPlatoon(PlayerID,Graveyard,myArmySize)
        end if
    end if
end while

delete Flag
delete Foundation

end script UA_SpawnUndeadPlatoonCheck


begin script UA_CreateUndeadPlatoon(PlayerID,Graveyard,howManyVillagers)

    ArmyFlag = 0
    ArmySize = 0
    Ghost[999]
    counter = 0
    maxTries = 0
    myEffect = 0
    myArmy = 0
    RelativePos = 0
    Villager = 0
    VillagerForNavigation = 0
    VillagerTemp = 0

start

    UA_myUsedUndead[PlayerID] += howManyVillagers
    begin loop
        RelativePos = marker at get world position from Graveyard to {number from -10 to 10,number from -10 to 10}
        VillagerForNavigation = get living villager at {Graveyard} radius 250
        maxTries++
        until VillagerForNavigation can navigate to {RelativePos} or maxTries > 100
    end loop

    if maxTries > 100
        RelativePos = marker at get world position from Graveyard to {4,4}
    end if
    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {RelativePos} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    myEffect = create visual effect VISUAL_EFFECT_TOWN_TAKEOVER_DEFENDER at {RelativePos} + {0,2,0} time 10
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 1.0

    myEffect = create visual effect VISUAL_MIRACLE_HEAL_GROUND at {RelativePos} + {0,2,0} time 20
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 2

    wait 3.33 seconds
    myArmy = create platoon PLATOON_INFO_GREEK_MELEE_5 at {RelativePos} with howManyVillagers men and 0 women
    attach 3d sound "SCRIPT24_SPOOKYLOOP" to myArmy with volume 1.0
    PLAYER of myArmy = PlayerID
    attach atmos "ghost_legion" to myArmy range 40
    disable platoon myArmy merge
    disable platoon myArmy split
    disable platoon myArmy can disband
    disable platoon myArmy camp

    //set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_MASS of myArmy to 1.0
    set platoon movement value FORMATION_MOVEMENT_STAT_ACCELERATION_MAX of myArmy to 30.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MAX of myArmy to 9.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_SPEED_MIN of myArmy to 5.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_CHARGE_SPEED of myArmy to 10.0
    set platoon movement value FORMATION_MOVEMENT_STAT_AGENT_FIGHTER_SPEED of myArmy to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_SPEED of myArmy to 7.0
    set platoon movement value FORMATION_MOVEMENT_STAT_PLATOON_CHARGE_SPEED of myArmy to 10.0

    ArmyFlag = get SCRIPT_OBJECT_TYPE_ARMY_FLAG at {myArmy} radius 50
    override mesh for ArmyFlag with "..\models\m_bannerundead"
    disable platoon myArmy camp

    ArmySize = get number of villagers in platoon myArmy
    force while get number of villagers in platoon myArmy > 1
        Villager = get villager from platoon myArmy nearest {myArmy}
        play anim "a_p_dead1" on Villager loop 1
        delete Villager
    end while
    Villager = get villager from platoon myArmy nearest {myArmy}
    SCALE of Villager = 0.01
    while counter < howManyVillagers and counter < ArmySize
        //Create ghosts
        Ghost[counter] = create VILLAGER AZTEC_FORESTER at {myArmy} + {number from -10 to 10,number from -10 to 10}
        run background script UA_GhostScript(PlayerID,Ghost[counter],myArmy)
        myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Ghost[counter]} + {0,1,0} time 1.5
        set myEffect color red 148 green 0 blue 211
        SCALE of myEffect = 0.1
        add villager Ghost[counter] to platoon myArmy
        disable anim events on Ghost[counter]
        disable Ghost[counter] hurt by fire
        disable Ghost[counter] set on fire
        counter++
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {Villager} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    play anim "a_p_dead1" on Villager loop 1
    delete Villager

end script UA_CreateUndeadPlatoon


begin script UA_GhostScript(PlayerID,myGhost,myArmy)

    myEffect = 0
    PlayerAlignment = 0

start

    while myGhost exists and HEALTH of myGhost > 0.00 and PLAYER of myGhost == 0
        if SPEED of myGhost < 1.0
            SPEED of myGhost = 1.0
        end if
        override myGhost anim "a_p_run_male_miserable"
        //set anim speed of myGhost to 0.5
        override mesh for myGhost with "..\skins\s_skeletonadultland2"
        set myGhost carrying CARRIED_OBJECT_NONE
        set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
        wait until HEALTH of myGhost == 0 or 60 seconds
    end while

    myEffect = create visual effect VISUAL_EFFECT_WEATHER_LIGHTNING_EXPLOSION at {myGhost} + {0,1,0} time 1.5
    set myEffect color red 148 green 0 blue 211
    SCALE of myEffect = 0.1
    HEALTH of myGhost = 0.00

    begin loop
        //set anim speed of myGhost to 0.5
        override mesh for myGhost with "..\skins\s_skeletonadultland2"
        set myGhost carrying CARRIED_OBJECT_NONE
        set myGhost carrying CARRIED_OBJECT_NORSE_SWORD
        wait until HEALTH of myGhost == 0 or 60 seconds
        until not myGhost exists
    end loop

    //In case it gets disbanded, delete the ghost
    if myGhost exists
        play anim "a_p_dead1" on myGhost loop 1
        delete myGhost
    end if

end script UA_GhostScript
